% txrfract.mf
%
% (c) Copyright 1995, 1996, 1997 J"org Knappen
%
% This file is part of ecfonts version 1.0
%
% Please read the files 00readme.txt, 00inst.txt, 00error.txt, and
% copyrite.txt for further information
%
% You find some documentation in ecdoc.tex (needs LaTeX2e)
%
% Ready made fractions 1/2, 1/4, 3/4 in roman style

version_check(1,0);  % |version_check| was introduced in dc1.3

ecchar"Fraction one quarter";
beginchar(oct"274",8u#,body_height#,desc_depth#);
italcorr body_height#*slant-.5u#;
adjust_fit(0,0);
numeric c_height; c_height=good.y .5[h,-d]+.5rule_thickness;
numeric light_stem; light_stem=hround .4[stem',cap_stem'];
pickup tiny.nib;
pos1(light_stem,0); pos2(light_stem,0);
lft x1l=lft x2l=hround(.5(w+.5u)-.5cap_stem');
top y1=h+o; bot y2=c_height+rule_thickness;
filldraw stroke z1e--z2e;  % stem
if not serifs: save slab; slab=bar; fi
dish_serif(2,1,a,1/3,min(2.25u,lft x2l-1.5u),
  b,1/3,min(2.25u,w-1.25u-rt x2r)); % serif
pickup crisp.nib; pos3(slab,-90); pos4(bar,-90);
top y3l=h+o; top y4l=if monospace: .8 else: .9 fi\\ [bot y2,h]+o;
lft x4=max(1.25u,tiny.lft x1l-2.35u);
tiny.rt x1r=lft x3+.25[tiny,hair];
erase fill z3l{x4l-x3l,3(y4l-y3l)}...z4l{left}
 --(x4l,h+o+1)--(x3l,h+o+1)--cycle; % erase excess at top
filldraw stroke z3e{x4e-x3e,3(y4e-y3e)}..z4e{left};  % point
penlabels(1,2,3,4);
clearxy;
numeric hh; hh=c_height-2rule_thickness;
numeric light_stem, light_stem', diag_stem, alpha, cut; cut=.75notch_cut;
light_stem=hround .4[fudged.stem,fudged.cap_stem];
light_stem'=hround max(tiny.breadth,light_stem-2stem_corr);
diag_stem=max(tiny.breadth,.4[vair,fudged.hair]);
pickup crisp.nib; pos5(cap_bar,90); pos6(cap_bar,90);
lft x5=hround .5u; rt x6=hround(w-.5u);
top y5r=vround(if serifs: 5/18[slab-d,hh-light_stem]+1
 else:.35[-d,hh-light_stem] fi+.5cap_bar);
z4l=top lft z5r; y2=y2'=y5=y6; x1r=x2r=hround(w-3u+.5light_stem);
penpos1(light_stem',0); penpos2(light_stem',0); y1=y3=hh+apex_o+apex_oo;
x3r+apex_corr=x1r; alpha=diag_ratio(1,diag_stem,y3-y4l,x3r-x4l);
penpos3(alpha*diag_stem,0); penpos4(alpha*diag_stem,0);
x0=x1l; z0=whatever[z3r,z4r];
x5'=x5; z5''=z5'+penoffset z4-z3 of currentpen=whatever[z4l,z3l];
fill diag_end(2r,1r,1,.5,3l,4l)---z5''...lft z5'
 ---lft z5l -- (x4r,y5l) -- z4r
 if y0<hh-cut:{z3r-z4r}...{up}(x1l-1,hh-cut)
  --(x1l,hh-cut) else: -- z0 fi
 --z2l--z2r--cycle;  % diagonal and upper stem
filldraw stroke z5e--z6e;  % bar
pickup tiny.nib; pos7(light_stem,0); rt x7r=x1r; bot y7=-d;
pos2'(light_stem,0); x2'=x7;
filldraw stroke z2'e--z7e;  % lower stem
if serifs: dish_serif(7,2',a,1/3,1.75u,
 b,1/3,min(1.75u,w-.5u-rt x7r)); fi  % serif
penlabels(0,1,2,3,4,5,6,7);
pickup rule.nib;
top y98=top y99=c_height; x98=w-x99=hround .5u;
draw z98--z99; % fraction bar
labels(98,99); endchar;

ecchar"Fraction one half";
beginchar(oct"275",8u#,body_height#,desc_depth#);
italcorr body_height#*slant-.5u#;
adjust_fit(0,0);
numeric c_height; c_height=good.y .5[h,-d]+.5rule_thickness;
numeric light_stem; light_stem=hround .4[stem',cap_stem'];
pickup tiny.nib;
pos1(light_stem,0); pos2(light_stem,0);
lft x1l=lft x2l=hround(.5(w+.5u)-.5cap_stem');
top y1=h+o; bot y2=c_height+rule_thickness;
filldraw stroke z1e--z2e;  % stem
if not serifs: save slab; slab=bar; fi
dish_serif(2,1,a,1/3,min(2.25u,lft x2l-1.5u),
  b,1/3,min(2.25u,w-1.25u-rt x2r)); % serif
pickup crisp.nib; pos3(slab,-90); pos4(bar,-90);
top y3l=h+o; top y4l=if monospace: .8 else: .9 fi\\ [bot y2,h]+o;
lft x4=max(1.25u,tiny.lft x1l-2.35u);
tiny.rt x1r=lft x3+.25[tiny,hair];
erase fill z3l{x4l-x3l,3(y4l-y3l)}...z4l{left}
 --(x4l,h+o+1)--(x3l,h+o+1)--cycle; % erase excess at top
filldraw stroke z3e{x4e-x3e,3(y4e-y3e)}..z4e{left};  % point
penlabels(1,2,3,4);
clearxy;
numeric arm_thickness, hair_vair; hair_vair=.25[vair,hair];
arm_thickness=Vround(if hefty:slab+2stem_corr else:.4[stem,cap_stem] fi);
pickup crisp.nib; pos7(arm_thickness,-90); pos8(hair,0);
bot y7r=-d; lft x7=hround .9u; rt x8r=hround(w-.9u);
y8=good.y(y7l+beak/2)+eps;
arm(7,8,a,.3beak_darkness,beak_jut);  % arm and beak
pickup fine.nib; pos2(slab,90); pos3(.4[curve,cap_curve],0);
top y2r=c_height-2rule_thickness+o;
x2=.5(w-.5u); rt x3r=hround(w-.9u); y3+.5vair=.75[-d,top y2r];
if serifs: numeric bulb_diam; bulb_diam=hround(flare+2/3(cap_stem-stem));
 pos0(bulb_diam,180); pos1(cap_hair,180);
 lft x1r=hround .9u; y1-.5bulb_diam=2/3[-d,top y2r];
 (x,y2l)=whatever[z1l,z2r]; x2l:=x; bulb(2,1,0);  % bulb and arc
else: x2l:=x2l-.25u; pos1(flare,angle(-9u,h));
 lft x1r=hround .75u; bot y1l=vround .7[-d,top y2r];
 y1r:=good.y y1r+eps; x1l:=good.x x1l;
 filldraw stroke term.e(2,1,left,.9,4); fi  % terminal and arc
pos4(.25[hair_vair,cap_stem],0);
pos5(hair_vair,0); pos6(hair_vair,0);
y5=arm_thickness-d; y4=.3[y5,y3]; top y6=min(y5,slab-d,top y7l);
lft x6l=crisp.lft x7; z4l=whatever[z6l,(x3l,bot .58[-d,top y2r])];
z5l=whatever[z6l,z4l];
erase fill z4l--z6l--lft z6l--(lft x6l,y4l)--cycle;  % erase excess at left
filldraw stroke z2e{right}..tension atleast .9 and atleast 1
 ..z3e{down}.. z4e---z5e--z6e;  % stroke
penlabels(0,1,2,3,4,5,6,7,8);
pickup rule.nib;
top y98=top y99=c_height; x98=w-x99=hround .5u;
draw z98--z99; % fraction bar
labels(98,99); endchar;

ecchar"Fraction three quarters";
beginchar(oct"276",8u#,body_height#,desc_depth#);
italcorr body_height#*slant-.5u#;
adjust_fit(0,0);
numeric c_height; c_height=good.y .5[h,-d]+.5rule_thickness;
numeric top_thickness,mid_thickness,bot_thickness;
top_thickness=max(fine.breadth,vround(slab-2vair_corr));
mid_thickness=max(fine.breadth,vround 2/3vair);
bot_thickness=max(fine.breadth,vround(slab-vair_corr));
pickup fine.nib; pos2(top_thickness,90); top y2r=h+o;
pos3(max(fine.breadth,.6[curve,cap_curve]-stem_corr),0);
rt x3r=hround(w-1.25u);
pos4(vair,-90); pos5(vair,-90);
pos6(mid_thickness,90); x2=x6=x8=.5[1.5u,x7];
pos7(cap_curve,0); rt x7r=hround(w-.75u); lft x5=min(hround 3u,lft x6)-eps;
pos8(bot_thickness,-90); bot y8r=c_height+rule_thickness-o;
y3=.6[top y4l,bot y2l]; y7=.5[bot y6l,top y8l];
top y5l=vround(.54[bot y8r,h]+.5vair); y5r=y6l;
x4=1/3[x5,x3l]; z4=z5+whatever*(150u,h);
filldraw stroke pulled_super_arc.e(2,3)(.5superpull)
 & z3e{down}...z4e---z5e;  % upper bowl
filldraw z5r--z6l--z6r--z5l---cycle;  % middle tip
filldraw stroke pulled_super_arc.e(6,7)(.5superpull)
 & pulled_super_arc.e(7,8)(.5superpull);  % lower bowl
if serifs: numeric bulb_diam[];
 bulb_diam1=flare+.5(cap_stem-stem); bulb_diam2=flare+cap_stem-stem;
 pos0(bulb_diam1,180); pos1(hair,180);
 lft x0r=hround 1.25u;
 y0=min(.9[bot y8r,h]-.5bulb_diam1,.75[bot y8r,h]+.5bulb_diam1);
 bulb(2,1,0);  % upper bulb
 pos10(bulb_diam2,-180); pos9(cap_hair,-180);
 lft x10r=hround .75u;
 y10=max(.1[bot y8r,h]+.5bulb_diam2,.3[bot y8r,h]-.5bulb_diam2);
 bulb(8,9,10);  % lower bulb
else: pos1(.5[vair,flare],angle(-8u,h));
 lft x1r=hround u; bot y1l=vround .75[bot y8r,h]+o;
 y1r:=good.y y1r+eps; x1l:=good.x x1l;
 pos9(bot_thickness,angle(-2u,-h));
 lft x9r=hround .75u; top y9l=vround .25[bot y8r,h]-o;
 y9r:=good.y y9r-eps; x9l:=good.x x9l;
 filldraw stroke term.e(2,1,left,1,4);  % upper terminal
 filldraw stroke term.e(8,9,left,1,4); fi  % lower terminal
penlabels(0,1,2,3,4,5,6,7,8,9,10);
clearxy;
numeric hh; hh=c_height-2rule_thickness;
numeric light_stem, light_stem', diag_stem, alpha, cut; cut=.75notch_cut;
light_stem=hround .4[fudged.stem,fudged.cap_stem];
light_stem'=hround max(tiny.breadth,light_stem-2stem_corr);
diag_stem=max(tiny.breadth,.4[vair,fudged.hair]);
pickup crisp.nib; pos5(cap_bar,90); pos6(cap_bar,90);
lft x5=hround .5u; rt x6=hround(w-.5u);
top y5r=vround(if serifs: 5/18[slab-d,hh-light_stem]+1
 else:.35[-d,hh-light_stem] fi+.5cap_bar);
z4l=top lft z5r; y2=y2'=y5=y6; x1r=x2r=hround(w-3u+.5light_stem);
penpos1(light_stem',0); penpos2(light_stem',0); y1=y3=hh+apex_o+apex_oo;
x3r+apex_corr=x1r; alpha=diag_ratio(1,diag_stem,y3-y4l,x3r-x4l);
penpos3(alpha*diag_stem,0); penpos4(alpha*diag_stem,0);
x0=x1l; z0=whatever[z3r,z4r];
x5'=x5; z5''=z5'+penoffset z4-z3 of currentpen=whatever[z4l,z3l];
fill diag_end(2r,1r,1,.5,3l,4l)---z5''...lft z5'
 ---lft z5l -- (x4r,y5l) -- z4r
 if y0<hh-cut:{z3r-z4r}...{up}(x1l-1,hh-cut)
  --(x1l,hh-cut) else: -- z0 fi
 --z2l--z2r--cycle;  % diagonal and upper stem
filldraw stroke z5e--z6e;  % bar
pickup tiny.nib; pos7(light_stem,0); rt x7r=x1r; bot y7=-d;
pos2'(light_stem,0); x2'=x7;
filldraw stroke z2'e--z7e;  % lower stem
if serifs: dish_serif(7,2',a,1/3,1.75u,
 b,1/3,min(1.75u,w-.5u-rt x7r)); fi  % serif
pickup rule.nib;
top y98=top y99=c_height; x98=w-x99=hround .5u;
draw z98--z99; % fraction bar
labels(98,99); endchar;

endinput;
