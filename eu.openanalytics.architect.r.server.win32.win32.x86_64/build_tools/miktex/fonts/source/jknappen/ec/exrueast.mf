% exrueast.mf
%
% (c) Copyright 1995, 1996, 1997 J"org Knappen
% (c) Copyright 1990, 1992 Norbert Schwarz
%
% This file is part of ecfonts version 1.0
%
% Please read the files 00readme.txt, 00inst.txt, 00error.txt, and
% copyrite.txt for further information
%
% You find some documentation in ecdoc.tex (needs LaTeX2e)
%
% Content:
%
%   uppercase accented letters (common to roman and italic fonts)
%   "eastern part"
%
%   oct"200" .. oct"235"
%
%   Roman uppercase accents
%
%       oct"200"    A  with breve
%       oct"201"    A  with ogonek
%       oct"202"    C  with acute
%       oct"203"    C  with hachek
%       oct"204"    D  with hachek
%       oct"205"    E  with hachek
%       oct"206"    E  with ogonek
%       oct"207"    G  with breve
%
%       oct"210"    L  with acute
%       oct"211"    L  with komma
%       oct"212"    L  with stroke (polish)
%       oct"213"    N  with acute
%       oct"214"    N  with hachek
%       oct"215"    The letter Eng
%       oct"216"    O  with hungarian umlaut
%       oct"217"    R  with acute
%
%       oct"220"    R  with hachek
%       oct"221"    S  with acute
%       oct"222"    S  with hachek
%       oct"223"    S  with cedilla
%       oct"224"    T  with hachek
%       oct"225"    T  with cedilla
%       oct"226"    U  with hungarian umlaut
%       oct"227"    U  with ring
%
%       oct"230"    Y  with umlauts
%       oct"231"    Z  with acute
%       oct"232"    Z  with hachek
%       oct"233"    Z  with dot
%       oct"234"    IJ
%       oct"235"    I  with dot

version_check(1,0);  % |version_check| was introduced in dc1.3

ecchar "The letter A with breve";
beginchar(oct"200",13u#,cap_height#+acc_height#,0);
adjust_fit(cap_serif_fit#,cap_serif_fit#);
numeric left_stem,right_stem,outer_jut,alpha;
right_stem=cap_stem-stem_corr;
left_stem=min(cap_hair if hefty: -3stem_corr fi,right_stem);
outer_jut=.8cap_jut; x1l=w-x4r=l+letter_fit+outer_jut+.5u; y1=y4=0;
x2-x1=x4-x3; x3r=x2r+apex_corr; y2=y3=cap_height+apex_o+apex_oo;
alpha=diag_ratio(2,left_stem,y2-y1,x4r-x1l-apex_corr);
penpos1(alpha*left_stem,0); penpos2(alpha*left_stem,0);
penpos3(alpha*right_stem,0); penpos4(alpha*right_stem,0);
z0=whatever[z1r,z2r]=whatever[z3l,z4l];
if y0<cap_height-cap_notch_cut: y0:=cap_height-cap_notch_cut;
 fill z0+.5right{down}...{z4-z3}diag_end(3l,4l,1,1,4r,3r)
  --diag_end(4r,3r,1,1,2l,1l)--diag_end(2l,1l,1,1,1r,2r){z2-z1}
  ...{up}z0+.5left--cycle; % left and right diagonals
else: fill z0--diag_end(0,4l,1,1,4r,3r)--diag_end(4r,3r,1,1,2l,1l)
  --diag_end(2l,1l,1,1,1r,0)--cycle; fi % left and right diagonals
penpos5(whatever,angle(z2-z1)); z5=whatever[z1,z2];
penpos6(whatever,angle(z3-z4)); z6=whatever[z3,z4]; y6=y5;
if hefty: y5r else: y5 fi =5/12y0;
y5r-y5l=y6r-y6l=cap_band; penstroke z5e--z6e; % bar line
if serifs: numeric inner_jut; pickup tiny.nib;
 prime_points_inside(1,2); prime_points_inside(4,3);
 if rt x1'r+cap_jut+.5u+1<=lft x4'l-cap_jut: inner_jut=cap_jut;
 else: rt x1'r+inner_jut+.5u+1=lft x4'l-inner_jut; fi
 dish_serif(1',2,a,1/2,outer_jut,b,.6,inner_jut)(dark);  % left serif
 dish_serif(4',3,c,1/2,inner_jut,d,1/3,outer_jut); fi  % right serif
% the accent
uppercase_breve(x2,0,12,13,14);
penlabels(0,1,2,3,4,5,6,7,8,9,10,11,12,13,14); endchar;


%%% fine fisp
ecchar "The letter A with ogonek"; % from pl_dl (plfonts)
beginchar(oct"201",13u#,cap_height#,the_ogonek_depth#);
 adjust_fit(cap_serif_fit#,cap_serif_fit#);
 numeric left_stem,right_stem,outer_jut,alpha;
 right_stem=cap_stem-stem_corr;
 left_stem=min(cap_hair if hefty: -3stem_corr fi,right_stem);
 outer_jut=.8cap_jut; x1l=w-x4r=l+letter_fit+outer_jut+.5u; y1=y4=0;
 x2-x1=x4-x3; x3r=x2r+apex_corr; y2=y3=h+apex_o+apex_oo;
 alpha=diag_ratio(2,left_stem,y2-y1,x4r-x1l-apex_corr);
 penpos1(alpha*left_stem,0); penpos2(alpha*left_stem,0);
 penpos3(alpha*right_stem,0); penpos4(alpha*right_stem,0);
 z0=whatever[z1r,z2r]=whatever[z3l,z4l];
 if y0<h-cap_notch_cut: y0:=h-cap_notch_cut;
  fill z0+.5right{down}...{z4-z3}diag_end(3l,4l,1,1,4r,3r)
   --diag_end(4r,3r,1,1,2l,1l)--diag_end(2l,1l,1,1,1r,2r){z2-z1}
   ...{up}z0+.5left--cycle; % left and right diagonals
 else: fill z0--diag_end(0,4l,1,1,4r,3r)--diag_end(4r,3r,1,1,2l,1l)
   --diag_end(2l,1l,1,1,1r,0)--cycle; fi % left and right diagonals
 penpos5(whatever,angle(z2-z1)); z5=whatever[z1,z2];
 penpos6(whatever,angle(z3-z4)); z6=whatever[z3,z4]; y6=y5;
 if hefty: y5r else: y5 fi =5/12y0;
 y5r-y5l=y6r-y6l=cap_band; penstroke z5e--z6e; % bar line
 if serifs: numeric inner_jut; pickup tiny.nib;
  prime_points_inside(1,2); prime_points_inside(4,3);
  if rt x1'r+cap_jut+.5u+1<=lft x4'l-cap_jut: inner_jut=cap_jut;
  else: rt x1'r+inner_jut+.5u+1=lft x4'l-inner_jut; fi
  dish_serif(1',2,a,1/2,outer_jut,b,.6,inner_jut)(dark); % left serif
  dish_serif(4',3,c,1/2,inner_jut,d,1/3,outer_jut); fi % right serif
 penlabels(0,1,2,3,4,5,6);
 def the_pen=if known ogonek_pen: ogonek_pen
  elseif serifs: tiny elseif is_bold: fisp else: fine fi
 enddef;
 ogonek_breadth=if serifs and is_bold: slab else: xvair fi;
% |diag_end| refers to |tiny| pen, hence |tiny| in the following equation:
 ogonek_pos=(x4 if not serifs: l if is_bold: +.75tiny fi\\ fi,.5the_pen)
   if serifs and not is_egyptian: +(.5xvair,0) fi;
 put_ogonek(the_pen);
endchar;

ecchar "The letter C with acute"; % from pl_dl (plfonts)
if serifs:
 beginchar_twice(oct"202",13u#,cap_height#,0);
 italcorr cap_height#*slant-.5u#;
 adjust_fit(0,0);
 pickup fine.nib; pos1(cap_hair,0); pos2(cap_band,90);
 pos3(cap_curve,180); pos4(cap_band,270); pos5(hair,360);
 rt x1r=rt x5r=hround(w-u); lft x3r=hround u; x2=x4=.55[x3,x1];
 top y2r=h+o; bot y4r=-o; y3=.5[y2,y4];
 bot y1=min(vround max(.6h,x_height-.5vair),bot y2l-eps);
 y5=max(good.y .95(h-y1),y4l+eps);
 (x2l',y2l)=whatever[z2r,z1l]; x2l:=min(x2l',x2l+.5u);
 (x4l',y4l)=whatever[z4r,z5l]; x4l:=min(x4l',x4l+.5u);
 filldraw stroke z1e{x2-x1,10(y2-y1)}
  ...pulled_arc.e(2,3) & pulled_arc.e(3,4)...{up}z5e; % arc
 pos6(.3[fine.breadth,cap_hair],0); x6r=x1r; top y6=h+o;
 x1r-x1'=2cap_curve-fine; y1'=y1;
 path upper_arc; upper_arc=z1{x2-x1,10(y2-y1)}..z2{left};
 numeric t; t=xpart(upper_arc intersectiontimes (z6l--z1'));
 filldraw z1r--z6r--z6l--subpath(t,0) of upper_arc--cycle; % barb
 penlabels(1,1',2,3,4,5,6);
 put_accent;
 repeat_once;
else: beginchar_twice(oct"202",11.5u#,cap_height#,0);
 italcorr cap_height#*slant-.5u#;
 adjust_fit(0,0);
 pickup fine.nib; pos1(1.2flare,80); pos2(slab,90);
 pos3(cap_curve,180); pos4(slab,270); pos5(flare,275);
 rt x1r=hround(w-1.1u); x2=x4=.5w+1.25u;
 lft x3r=hround max(u,2u-.5cap_curve); rt x5r=hround(w-.9u);
 top y1r=vround .95h+o; top y2r=h+o; y3=.5h;
 bot y4r=-o; bot y5r=vround .08h-o; y5l:=good.y y5l; x5l:=good.x x5l;
 filldraw stroke rterm.e(2,1,right,.9,4) & super_arc.e(2,3)
  & super_arc.e(3,4) & term.e(4,5,right,.8,4); % arc and terminals
 penlabels(1,1',2,3,4,5,6);
 put_accent;
 repeat_once;
fi
endchar; 

ecchar "The letter C with hachek"; % from cshacheu.mf (csfonts)
if serifs: beginchar(oct"203",13u#,cap_height#+acc_height#,0);
 italcorr cap_height#*slant-.5u#;
 adjust_fit(0,0);
 pickup fine.nib; pos1(cap_hair,0); pos2(cap_band,90);
 pos3(cap_curve,180); pos4(cap_band,270); pos5(hair,360);
 rt x1r=rt x5r=hround(w-u); lft x3r=hround u; x2=x4=.55[x3,x1];
 top y2r=cap_height+o; bot y4r=-o; y3=.5[y2,y4];
 bot y1=min(vround max(.6cap_height,x_height-.5vair),bot y2l-eps);
 y5=max(good.y .95(cap_height-y1),y4l+eps);
 (x2l',y2l)=whatever[z2r,z1l]; x2l:=min(x2l',x2l+.5u);
 (x4l',y4l)=whatever[z4r,z5l]; x4l:=min(x4l',x4l+.5u);
 filldraw stroke z1e{x2-x1,10(y2-y1)}
  ...pulled_arc.e(2,3) & pulled_arc.e(3,4)...{up}z5e; % arc
 pos6(.3[fine.breadth,cap_hair],0); x6r=x1r; top y6=cap_height+o;
 x1r-x1'=2cap_curve-fine; y1'=y1;
 path upper_arc; upper_arc=z1{x2-x1,10(y2-y1)}..z2{left};
 numeric t; t=xpart(upper_arc intersectiontimes (z6l--z1'));
 filldraw z1r--z6r--z6l--subpath(t,0) of upper_arc--cycle; % barb
else: beginchar(oct"203",11.5u#,cap_height#+acc_height#,0);
 italcorr cap_height#*slant-.5u#;
 adjust_fit(0,0);
 pickup fine.nib; pos1(1.2flare,80); pos2(slab,90);
 pos3(cap_curve,180); pos4(slab,270); pos5(flare,275);
 rt x1r=hround(w-1.1u); x2=x4=.5w+1.25u;
 lft x3r=hround max(u,2u-.5cap_curve); rt x5r=hround(w-.9u);
 top y1r=vround .95cap_height+o; top y2r=cap_height+o; y3=.5cap_height;
 bot y4r=-o; bot y5r=vround .08cap_height-o; y5l:=good.y y5l; x5l:=good.x x5l;
 filldraw stroke rterm.e(2,1,right,.9,4) & super_arc.e(2,3)
  & super_arc.e(3,4) & term.e(4,5,right,.8,4); fi % arc and terminals
math_fit(-.3cap_height#*slant-.5u#,.5ic#);
penlabels(1,1',2,3,4,5,6);
uc_hachek_accent(x2l);
%uc_hachek_accent(0.6[x3,x1]);    %% changed from .5 to .65  % 25/12/91
endchar;             %% and to .6  % 3/1/92 %% changed via lc  11/3/92


ecchar "The letter D with hachek"; % from cshacheu.mf (csfonts)
beginchar(oct"204",13.5u#,cap_height#+acc_height#,0);
italcorr .7cap_height#*slant-.5u#;
adjust_fit(cap_serif_fit#,0);
pickup tiny.nib; pos1(cap_stem',0); pos2(cap_stem',0);
lft x1l=lft x2l=hround max(2u,3u-.5cap_stem');
top y1=cap_height; bot y2=0;
filldraw stroke z1e--z2e; % stem
penpos3(cap_band,90); penpos4(cap_band,90);
penpos5(cap_curve-stem_corr,0);
penpos6(cap_band,-90); penpos7(cap_band,-90);
z3r=top z1; y4=y3; y5=.51[y4,y6]; y6=y7;
z7r=bot z2; x4=x6=.5w+.25u; x5r=hround(w-u);
x4l:=x6l:=x4-.25cap_curve;
fill stroke z3e..pulled_arc.e(4,5) & pulled_arc.e(5,6)..z7e;  % lobe
if serifs: nodish_serif(1,2,a,1/3,cap_jut,b,1/3,.5cap_jut);  % upper serif
 nodish_serif(2,1,c,1/3,cap_jut,d,1/3,.5cap_jut); fi  % lower serif
math_fit(0,ic#-.5u#); penlabels(1,2,3,4,5,6,7);
uc_hachek_accent(1/3[x1l,x5r]);
endchar;


ecchar "The letter E with hachek"; % from cshacheu.mf (csfonts)
beginchar(oct"205", 12u#-width_adj#,cap_height#+acc_height#,0);
italcorr cap_height#*slant-beak_jut#-.5u#;
adjust_fit(cap_serif_fit#,0);
hh:=vround(cap_height-stem_corr);
pickup tiny.nib; pos1(cap_stem,0); pos2(cap_stem,0);
lft x1l=lft x2l=hround max(2u,3u-.5cap_stem); top y1=hh; bot y2=0;
filldraw stroke z1e--z2e; % stem
pickup crisp.nib; pos3(slab,90); pos4(hair,0);
top y3r=hh; x3=x1; rt x4r=hround(w-u); y4=good.y(y3l-beak)-eps;
arm(3,4,e,beak_darkness,beak_jut);  % upper arm and beak
pos5(cap_bar,-90); pos6(hair,0);
top y5l=vround(.52[y2,y1]+.5cap_bar); x5=x1;
pos0(cap_bar,90); pos7(hair,0);
z0=z5; x6=x7; y6-y5l=y0l-y7;
if serifs: rt x6r=hround(w-4.4u+.5hair); y6=good.y(y5l+.6beak)+eps;
 rt x9r=hround(w-.5u);
else: rt x6r=hround(w-1.5u); y6=y5l+eps; rt x9r=hround(w-.75u); fi
arm(5,6,f,beak_darkness,0); arm(0,7,g,beak_darkness,0);  % middle arm and serif
pos8(slab if not serifs:+2stem_corr fi,-90); pos9(hair,0);
bot y8r=0; x8=x2; y9=good.y(y8l+7/6beak)+eps;
arm(8,9,hh,beak_darkness,1.5beak_jut);  % lower arm and beak
if serifs: nodish_serif(1,2,a,1/3,cap_jut,b,1/3,.5cap_jut);  % upper serif
 nodish_serif(2,1,c,1/3,cap_jut,d,1/3,.5cap_jut); fi  % lower serif
math_fit(0,.5ic#); penlabels(0,1,2,3,4,5,6,7,8,9);
uc_hachek_accent(if serifs: 0.4[x1l,x.e1] else: .5 [x1l,x4] fi);
endchar;


ecchar "The letter E with ogonek"; % from pl_dl (plfonts)
beginchar(oct"206",12u#-width_adj#,cap_height#,the_ogonek_depth#);
 italcorr cap_height#*slant-beak_jut#-.5u#;
 adjust_fit(cap_serif_fit#,0);
 h:=vround(h-stem_corr);
 pickup tiny.nib; pos1(cap_stem,0); pos2(cap_stem,0);
 lft x1l=lft x2l=hround max(2u,3u-.5cap_stem); top y1=h; bot y2=0;
 filldraw stroke z1e--z2e; % stem
 pickup crisp.nib; pos3(slab,90); pos4(hair,0);
 top y3r=h; x3=x1; rt x4r=hround(w-u); y4=good.y(y3l-beak)-eps;
 arm(3,4,e,beak_darkness,beak_jut); % upper arm and beak
 pos5(cap_bar,-90); pos6(hair,0);
 top y5l=vround(.52[y2,y1]+.5cap_bar); x5=x1;
 pos0(cap_bar,90); pos7(hair,0);
 z0=z5; x6=x7; y6-y5l=y0l-y7;
 if serifs: rt x6r=hround(w-4.4u+.5hair); y6=good.y(y5l+.6beak)+eps;
  rt x9r=hround(w-.5u);
 else: rt x6r=hround(w-1.5u); y6=y5l+eps; rt x9r=hround(w-.75u); fi
 arm(5,6,f,beak_darkness,0); arm(0,7,g,beak_darkness,0); % middle arm and serif
 pos8(slab if not serifs:+2stem_corr fi,-90); pos9(hair,0);
 bot y8r=0; x8=x2; y9=good.y(y8l+7/6beak)+eps;
 arm(8,9,h,beak_darkness,1.5beak_jut); % lower arm and beak
 if serifs: nodish_serif(1,2,a,1/3,cap_jut,b,1/3,.5cap_jut); % upper serif
  nodish_serif(2,1,c,1/3,cap_jut,d,1/3,.5cap_jut); fi % lower serif
 penlabels(0,1,2,3,4,5,6,7,8,9);
 def the_pen=if known ogonek_pen: ogonek_pen
  elseif serifs: tiny elseif is_bold: fisp else: fine fi
 enddef;
 ogonek_breadth=if serifs and is_bold: slab else: xvair fi;
 ogonek_pos=
  (x.h0-if serifs: 2 else: if is_bold: 1.5 else: 1.75 fi\\ fi\\ u,.5the_pen)+
  .5 ogonek_breadth*if is_egyptian: origin elseif serifs: right else: left fi;
 put_ogonek(the_pen);
endchar;

ecchar "The letter G with breve";
if serifs: beginchar(oct"207",14u#,cap_height#+acc_height#,0);
 italcorr cap_height#*slant-1.5u#-.5cap_serif_fit#;
 adjust_fit(0,.5cap_serif_fit#);
 pickup tiny.nib; pos0(cap_stem,0); pos7(cap_stem,0);
 rt x0r=hround(w-2u); y0=good.y(.1[bar_height,x_height]+1); x7=x0;
 pickup fine.nib;
 if hefty: bot y7=0; pos5(cap_stem,0); x5=x0; top y5=tiny.top y0;
 else: pos5(cap_hair,0); pos8(cap_stem,0);
  y7=.5bar_height; z5l=z8l; z8=z7; x9=x8r; bot y9=0;
  filldraw z8l{down}..{4(x9-x8),y9-y8}z9--z8r--cycle; fi % spur
 pos1(cap_hair,0); pos2(cap_band,90);
 pos3(cap_curve,180); pos4(cap_band,270);
 rt x1r=hround(w-2u); lft x3r=hround u; x2=x4=.55[x3,x1];
 top y2r=cap_height+o; bot y4r=-o; y3=.5[y2,y4];
 bot y1=min(vround max(.6h,x_height-.5vair),bot y2l-eps);
 (x2l',y2l)=whatever[z2r,z1l]; x2l:=min(x2l',x2l+.5u);
 (x4l',y4l)=whatever[z4r,z5l]; x4l:=min(x4l',x4l+.5u);
 filldraw stroke z1e{x2-x1,10(y2-y1)}
  ...pulled_arc.e(2,3) & pulled_arc.e(3,4)...z5e{up}; % arc
 pos6(.3[fine.breadth,cap_hair],0); x6r=x1r; top y6=cap_height+o;
 x1r-x1'=2cap_curve-fine; y1'=y1;
 path upper_arc; upper_arc=z1{x2-x1,10(y2-y1)}..z2{left};
 numeric t; t=xpart(upper_arc intersectiontimes (z6l--z1'));
 filldraw z1r--z6r--z6l--subpath(t,0) of upper_arc--cycle; % barb
 pickup tiny.nib; filldraw stroke z0e--z7e;  % stem
 dish_serif(0,7,a,1/3,max(cap_jut,2.25u),b,1/3,1.25u);  % serif
 math_fit(-.3cap_height#*slant-.5u#,ic#);
else: beginchar(oct"207",12u#,cap_height#+acc_height#,0);
 italcorr cap_height#*slant-.5u#;
 adjust_fit(0,0);
 pickup fine.nib; pos1(1.2flare,80); pos2(slab,90);
 pos3(cap_curve,180); pos4(slab,270); pos5(flare,270);
 rt x1r=hround(w-1.35u); x2=x4=.5w+u;
 lft x3r=hround max(u,2u-.5cap_curve); rt x5l=hround(w-1.2u);
 top y1r=vround .93cap_height+o; top y2r=cap_height+o; y3=.5cap_height;
 bot y4r=-o; bot y5r=vround .07cap_height-o;
 filldraw stroke rterm.e(2,1,right,.9,4)&super_arc.e(2,3)
  & super_arc.e(3,4) & term.e(4,5,right,1,4); % arc and terminals
 pos0(stem,0); pos7(stem,0);
 z7r=z5r; x0=x7; top y0=1+vround .35[bar_height,x_height];
 filldraw stroke z0e--z7e;  % stem
 pos8(cap_bar,90); pos9(cap_bar,90);
 z0r=z9r; y8=y9; lft x8=hround x4;
 filldraw stroke z8e--z9e;  % bar
 math_fit(-.3cap_height#*slant-.5u#,.5ic#); fi
% the accent
uppercase_breve(x2,0,10,11,12);
penlabels(0,1,1',2,3,4,5,6,7,8,9,10,11,12);endchar;


ecchar "The letter L with acute";
beginchar_twice(oct"210",11u#-width_adj#,cap_height#+acc_height#,0);
adjust_fit(cap_serif_fit#,0);
pickup tiny.nib; pos1(cap_stem,0); pos2(cap_stem,0);
lft x1l=lft x2l=hround max(2u,3u-.5cap_stem); top y1=cap_height; bot y2=0;
filldraw stroke z1e--z2e; % stem
pickup crisp.nib; pos3(slab,-90); pos4(hair,0);
bot y3r=0; x3=x2; y4=y3l+7/6beak+eps; rt x4r=hround(w-.75u);
arm(3,4,e,1.2beak_darkness,beak_jut);  % lower arm and beak
if serifs: dish_serif(1,2,a,1/3,cap_jut,b,1/3,1.25cap_jut);  % upper serif
 nodish_serif(2,1,c,1/3,cap_jut,d,1/3,.5cap_jut); fi  % lower serif
% the accent
hpos_corr=-.5w+hround max(2u,3u-.5cap_stem);
put_accent;
repeat_once;
math_fit(0,u#); penlabels(1,2,3,4); endchar;


ecchar "The letter L with hachek"; % from csotheru.mf (csfonts)
beginchar(oct"211",11u#-width_adj#,cap_height#+acc_height#,0);
adjust_fit(cap_serif_fit#,0);
pickup tiny.nib; pos1(cap_stem,0); pos2(cap_stem,0);
lft x1l=lft x2l=hround max(2u,3u-.5cap_stem); top y1=cap_height; bot y2=0;
filldraw stroke z1e--z2e; % stem
pickup crisp.nib; pos3(slab,-90); pos4(hair,0);
bot y3r=0; x3=x2; y4=y3l+7/6beak+eps; rt x4r=hround(w-.75u);
arm(3,4,e,1.2beak_darkness,beak_jut);  % lower arm and beak
if serifs: dish_serif(1,2,a,1/3,cap_jut,b,1/3,1.25cap_jut);  % upper serif
 nodish_serif(2,1,c,1/3,cap_jut,d,1/3,.5cap_jut); fi  % lower serif
math_fit(0,u#); penlabels(1,2,3,4);
dtl_hachek(x4-pdot_diam)(y1+.5pdot_diam);
endchar;

ecchar "The letter crossed L"; % from pl_dl (plfonts)
beginchar_twice(oct"212",11u#-width_adj#,cap_height#,0);
 adjust_fit(cap_serif_fit# if not serifs: +1.5u# fi,0);
 pickup tiny.nib; pos1(cap_stem,0); pos2(cap_stem,0);
 lft x1l=lft x2l=hround max(2u,3u-.5cap_stem); top y1=h; bot y2=0;
 filldraw stroke z1e--z2e; % stem
 pickup crisp.nib; pos3(slab,-90); pos4(hair,0);
 bot y3r=0; x3=x2; y4=y3l+7/6beak+eps; rt x4r=hround(w-.75u);
 arm(3,4,e,1.2beak_darkness,beak_jut); % lower arm and beak
 if serifs: dish_serif(1,2,a,1/3,cap_jut,b,1/3,1.25cap_jut); % upper serif
  nodish_serif(2,1,c,1/3,cap_jut,d,1/3,.5cap_jut); fi % lower serif
 penlabels(1,2,3,4);
% some funny fonts have an extremally small |cap_jut|:
 put_cross(.5cap_stem+if serifs: .9max(limit_dist,cap_jut) else: 1.8u fi,
  .5cap_stem+3u);
repeat_once;
endchar;

ecchar "The letter N with acute"; % from pl_dl (plfonts)
beginchar_twice(oct"213",13u#+width_adj#,cap_height#,0);
 italcorr cap_height#*slant-cap_serif_fit#+cap_jut#-2.5u#+min(.5cap_stem#,u#);
 adjust_fit(cap_serif_fit#,cap_serif_fit#);
 numeric thin_stem; thin_stem=hround(fudged.hair+stem_corr);
 pickup tiny.nib; pos1(thin_stem,0); pos2(thin_stem,0);
 pos3(thin_stem,0); pos4(thin_stem,0);
 pickup tiny.nib; top y1=top y3=h; bot y2=bot y4=0;
 x1=x2; x3=x4; x1l=w-x3r;
 rt x3r=hround min(w-2u,w-3u+.5fudged.cap_stem);
 filldraw stroke z1e--z2e; % left stem
 filldraw stroke z3e--z4e; % right stem
 if hefty: penpos5(fudged.cap_stem,0); penpos6(fudged.cap_stem,0);
  x5l=x1; x6r=x4; y5=h; y6=0;
  numeric upper_notch,lower_notch;
  upper_notch=h-cap_notch_cut; lower_notch=cap_notch_cut;
  x1'=rt x1r; z1'=whatever[z5l,z6l]; x4'=lft x4l; z4'=whatever[z5r,z6r];
  fill z5l..
   if y1'<upper_notch: {right}(x1'+1,upper_notch){down}... fi
   {z6-z5}diag_in(5l,6l,1,6r)--z6r..
   if y4'>lower_notch: {left}(x4'-1,lower_notch){up}... fi
   {z5-z6}diag_in(6r,5r,1,5l)--cycle; % diagonal
 else: penpos5(whatever,0); penpos6(whatever,90);
  z5l=z1l; z6l=z4l;
  z7=z6l+(max(eps,cap_stem-3stem_corr-tiny),0) rotated (angle(z5l-z6l)-90);
  z5r=z7+whatever*(z5l-z6l); z6r=z7+whatever*(z5l-z6l);
  filldraw stroke z5e..z6e; fi % diagonal
 if serifs: if hefty: serif(1,2,a,1/3,-cap_jut); % upper left serif
  else: serif(5,6,a,1/3,-cap_jut); fi % upper left serif
  dish_serif(2,1,b,1/2,cap_jut,c,1/2,cap_jut)(dark); % lower left serif
  dish_serif(3,4,e,1/2,cap_jut,f,1/2,cap_jut)(dark); fi % upper right serif
 penlabels(1,1',2,3,4,4',5,6,7);
 hpos_corr=-.75u; put_accent;
repeat_once;
endchar;

ecchar "The letter N with hachek"; % from cshacheu.mf (csfonts)
beginchar(oct"214",13u#+width_adj#,cap_height#+acc_height#,0);
italcorr cap_height#*slant-cap_serif_fit#+cap_jut#-2.5u#+min(.5cap_stem#,u#);
adjust_fit(cap_serif_fit#,cap_serif_fit#);
numeric thin_stem; thin_stem=hround(fudged.hair+stem_corr);
pickup tiny.nib; pos1(thin_stem,0); pos2(thin_stem,0);
pos3(thin_stem,0); pos4(thin_stem,0);
pickup tiny.nib; top y1=top y3=cap_height; bot y2=bot y4=0;
x1=x2; x3=x4; x1l=w-x3r;
rt x3r=hround min(w-2u,w-3u+.5fudged.cap_stem);
filldraw stroke z1e--z2e; % left stem
filldraw stroke z3e--z4e; % right stem
if hefty: penpos5(fudged.cap_stem,0); penpos6(fudged.cap_stem,0);
 x5l=x1; x6r=x4; y5=cap_height; y6=0;
 numeric upper_notch,lower_notch;
 upper_notch=cap_height-cap_notch_cut; lower_notch=cap_notch_cut;
 x1'=rt x1r; z1'=whatever[z5l,z6l]; x4'=lft x4l; z4'=whatever[z5r,z6r];
 fill z5l..
  if y1'<upper_notch: {right}(x1'+1,upper_notch){down}... fi
  {z6-z5}diag_in(5l,6l,1,6r)--z6r..
  if y4'>lower_notch: {left}(x4'-1,lower_notch){up}... fi
  {z5-z6}diag_in(6r,5r,1,5l)--cycle;  % diagonal
else: penpos5(whatever,0); penpos6(whatever,90);
 z5l=z1l; z6l=z4l;
 z7=z6l+(max(eps,cap_stem-3stem_corr-tiny),0) rotated (angle(z5l-z6l)-90);
 z5r=z7+whatever*(z5l-z6l); z6r=z7+whatever*(z5l-z6l);
 filldraw stroke z5e..z6e; fi  % diagonal
if serifs: if hefty: serif(1,2,a,1/3,-cap_jut); % upper left serif
 else: serif(5,6,a,1/3,-cap_jut); fi  % upper left serif
 dish_serif(2,1,b,1/2,cap_jut,c,1/2,cap_jut)(dark); % lower left serif
 dish_serif(3,4,e,1/2,cap_jut,f,1/2,cap_jut)(dark); fi  % upper right serif
math_fit(0,max(.5ic#-.5u#,0)); penlabels(1,1',2,3,4,4',5,6,7); 
uc_hachek_accent(.5[x2,x3]);
endchar;


ecchar "The letter Eng"; % from uppern.mf (fc fonts)
beginchar(oct"215",14u#,cap_height#,0);
adjust_fit(cap_serif_fit#,0);
italcorr cap_height#*slant;
pickup tiny.nib; pos1(cap_stem,0); pos2(cap_stem,0);
y1=2/3h; rt x1r=hround(w-u); x2=x1; y2=.21h;
if serifs: pos3(vair,-90); pos4(cap_hair,-180);
 pos5(flare+(cap_stem-stem),-180);
 bot y3r=-o; x3=.5[x4,x2]; y5=1/6h; rt x5l=hround 8.75u; z5r=z4r;
 filldraw stroke z1e--z2e&super_arc.e(2,3);  % right stem and arc
 bulb(3,4,5);  % bulb
else: filldraw stroke z1e--z2e;  % right stem
 pickup fine.nib; pos3(cap_stem,0); z3=z2;
 pos4(flare,angle(6.5u,-h)); pos5(1.1flare,-100);
 bot y4r=-o; x4r=.5[x5r,x3r]; lft x5r=hround 7.75u; bot y5r=vround.06h-o;
 filldraw stroke z3e{down}....term.e(4,5,left,1,4); fi % arc and terminal
pickup tiny.nib; pos6(cap_stem,0);pos7(cap_stem,0);
lft x6l=lft x7l=hround(max(2u,3u-.5cap_stem));top y6=h; bot y7=0;
filldraw stroke z6e--z7e;     %left stem 
pos8(min (cap_hair,cap_stem),0); pos9(cap_hair,-90); pos10(cap_stem,-180);
y8=y10=2/3h; top y9=h+o; rt x8=rt x6; rt x10l=rt x1r; x9=.5[x8,x10];
filldraw stroke z8e{up}...z9e...{down}z10e;  % big arc
if serifs:
  numeric right_jut;              
  if rt x7r+cap_jut+u+1<lft x4r: right_jut=cap_jut;
  else: x7r+right_jut+u+1=lft x4r; fi
  dish_serif(6,7,a,1/3,cap_jut,b,1/3,0);         % upper serif
  dish_serif(7,6,c,1/3,cap_jut,d,1/3,right_jut); % lower serif
fi
math_fit(0,.5ic#-.5u#); penlabels(1,2,3,4,5); endchar;

ecchar "The letter O with double acute";
beginchar_twice(oct"216",14u#-width_adj#,cap_height#+acc_height#,0);
italcorr .7cap_height#*slant-.5u#;
adjust_fit(0,0);
penpos1(vair',90); penpos3(vround(vair+1.5vair_corr),-90);
penpos2(cap_curve,180); penpos4(cap_curve,0);
if monospace: x2r=hround 1.5u;
 interim superness:=sqrt superness;  % make |"O"|, not |"0"|
else: x2r=hround u; fi
x4r=w-x2r; x1=x3=.5w; y1r=cap_height+o; y3r=-o;
y2=y4=.5cap_height-vair_corr; y2l:=y4l:=.52cap_height;
penstroke pulled_super_arc.e(1,2)(.5superpull)
 & pulled_super_arc.e(2,3)(.5superpull)
 & pulled_super_arc.e(3,4)(.5superpull)
 & pulled_super_arc.e(4,1)(.5superpull) & cycle;  % bowl
math_fit(-.3cap_height#*slant-.5u#,ic#-.5u#);
if monospace: hpos_corr=-2.5u; put_accent; hpos_corr=2.5u; put_accent;
else: hpos_corr=-1.5u; put_accent; hpos_corr=1.5u; put_accent; fi
repeat_once;
penlabels(1,2,3,4); endchar;


ecchar "The letter R with acute";
beginchar_twice(oct"217",if serifs: 12u#+.5max(2u#,cap_curve#)
 else:12.5u#-.5width_adj# fi,cap_height#+acc_height#,0);
italcorr .75cap_height#*slant- if serifs: 1.75 else: .5 fi\\ u#;
adjust_fit(cap_serif_fit#,0);
pickup tiny.nib; pos1(cap_stem',0); pos2(cap_stem',0);
lft x1l=lft x2l=hround max(2u,3u-.5cap_stem'); top y1=cap_height; bot y2=0;
filldraw stroke z1e--z2e; % stem
penpos3(cap_band,90); penpos4(cap_band,90);
penpos5(cap_curve if hefty:-3stem_corr fi,0);
penpos6(vair,-90); penpos7(vair,-90);
z3r=top z1; y4=y3; y5=.5[y4l,y6l]; y6=y7;
x7=x2; y7l=vround(.5cap_height+.5vair); x4=x6;
if serifs: x4=.5w-.5u; x5r=hround(w-2.25u);
else: x4=.5w+.5u; x5r=hround(w-u); fi
x4l:=x6l:=x4-.125cap_curve;
fill stroke z3e..pulled_arc.e(4,5) & pulled_arc.e(5,6)..z7e;  % lobe
if serifs: pos6'(vair,-90); pos0(cap_stem,180);
 pos8(cap_curve,180); pos9(vair,-90); pos10(hair,0);
 z6'=z6; lft x0r=lft x8r=hround(x5-2/3u-.5cap_curve);
 y8=1/3[y2,y7]; y0=3/5[y2,y7]; x9=.5[x8l,x10r];
 bot y9r=-o; rt x10r=hround(w-.05u); y10=1/4[y2,y7];
 filldraw stroke z6'e{right}..z0e---z8e....z9e{right}..z10e{up};  % tail
 nodish_serif(1,2,a,1/3,cap_jut,b,1/3,.5cap_jut);  % upper serif
 dish_serif(2,1,c,1/3,cap_jut,d,1/3,cap_jut);  % lower serif
else: penpos8(cap_stem-2stem_corr,0); penpos9(cap_stem,0);
 x8=x6+.5u; y8=y6; x9r=hround(w-.5u); y9=0;
 fill z8l--diag_end(8l,9l,.5,1,9r,8r)--z8r--cycle; fi  % tail
hpos_corr=-.75u;
put_accent;
repeat_once;
math_fit(0,.75ic#); penlabels(0,1,2,3,4,5,6,7,8,9,10); endchar;

ecchar "The letter R with hachek"; % from cshacheu.mf (csfonts)
beginchar(oct"220",if serifs: 12u#+.5max(2u#,cap_curve#)
 else:12.5u#-.5width_adj# fi,cap_height#+acc_height#,0);
italcorr .75cap_height#*slant- if serifs: 1.75 else: .5 fi\\ u#;
adjust_fit(cap_serif_fit#,0);
pickup tiny.nib; pos1(cap_stem',0); pos2(cap_stem',0);
lft x1l=lft x2l=hround max(2u,3u-.5cap_stem'); top y1=cap_height; bot y2=0;
filldraw stroke z1e--z2e; % stem
penpos3(cap_band,90); penpos4(cap_band,90);
penpos5(cap_curve if hefty:-3stem_corr fi,0);
penpos6(vair,-90); penpos7(vair,-90);
z3r=top z1; y4=y3; y5=.5[y4l,y6l]; y6=y7;
x7=x2; y7l=vround(.5cap_height+.5vair); x4=x6;
if serifs: x4=.5w-.5u; x5r=hround(w-2.25u);
else: x4=.5w+.5u; x5r=hround(w-u); fi
x4l:=x6l:=x4-.125cap_curve;
fill stroke z3e..pulled_arc.e(4,5) & pulled_arc.e(5,6)..z7e;  % lobe
if serifs: pos6'(vair,-90); pos0(cap_stem,180);
 pos8(cap_curve,180); pos9(vair,-90); pos10(hair,0);
 z6'=z6; lft x0r=lft x8r=hround(x5-2/3u-.5cap_curve);
 y8=1/3[y2,y7]; y0=3/5[y2,y7]; x9=.5[x8l,x10r];
 bot y9r=-o; rt x10r=hround(w-.05u); y10=1/4[y2,y7];
 filldraw stroke z6'e{right}..z0e---z8e....z9e{right}..z10e{up};  % tail
 nodish_serif(1,2,a,1/3,cap_jut,b,1/3,.5cap_jut);  % upper serif
 dish_serif(2,1,c,1/3,cap_jut,d,1/3,cap_jut);  % lower serif
else: penpos8(cap_stem-2stem_corr,0); penpos9(cap_stem,0);
 x8=x6+.5u; y8=y6; x9r=hround(w-.5u); y9=0;
 fill z8l--diag_end(8l,9l,.5,1,9r,8r)--z8r--cycle; fi  % tail
math_fit(0,.75ic#); penlabels(0,1,2,3,4,5,6,7,8,9,10); 
uc_hachek_accent(.9[x1,x6l]);      %(1/3[x1l,x5r]); oprava 24.10.92
endchar;


ecchar "The letter S with acute"; % from pl_dl (plfonts)
beginchar_twice(oct"221",10u#,cap_height#,0);
 italcorr cap_height#*slant-u#;
 adjust_fit(0,0);
 numeric theta; theta=90-angle(50u,h); slope:=-h/50u; % angle at middle
 numeric s_slab; s_slab=if serifs:slab else:vround .2[slab,cap_stem] fi;
 numeric ess'; ess'=max(fine.breadth,cap_ess);
 pickup fine.nib; pos2(max(fine.breadth,s_slab-vround vair_corr),-90);
 pos0(ess',theta); pos7(s_slab,-90);
 x2+x7=2x0=w; x7-x2=if serifs: u else: 0 fi; top y2l=h+o; bot y7r=-o;
 y0=.52h; lft x3l=hround u; rt x6r=hround(w-u);
 x3r-x3l=x6r-x6l=hround .5[s_slab,ess']-fine;
 ellipse_set(2l,3l,4l,0l); ellipse_set(2r,3r,4r,0r); y3=y3r;
 ellipse_set(7l,6l,5l,0l); ellipse_set(7r,6r,5r,0r); y6=y6r;
 filldraw stroke super_arc.e(2,3) & z3e{down}
  ..z4e---z5e..z6e{down} & super_arc.e(6,7); % main stroke
 if serifs: pos1(hair,180); pos8(hair,180);
  rt x1l=hround(w-1.5u); lft x8r=hround u;
  bot y1=vround 2/3h+1; top y8=vround 1/3h-1;
  filldraw stroke z1e{x2-x1,10(y2-y1)}....{left}z2e; % upper arc
  filldraw stroke z7e{left}....{up}z8e; % lower arc
  path upper_arc, lower_arc;
  upper_arc=z1{x2-x1,10(y2-y1)}....{left}z2;
  lower_arc=z7{left}....{up}z8;
  pos10(.3[fine.breadth,cap_hair],0); pos9(.3[fine.breadth,cap_hair],0);
  x10r=x1l; top y10=top y2l; x9l=x8r; bot y9=bot y7r;
  x1l-x1'=x8'-x8r=1.6cap_curve-fine; y1'=y1; y8'=y8;
  numeric t; t=xpart(upper_arc intersectiontimes(z10l--z1'));
  filldraw z1l--z10r--z10l--subpath(t,0) of upper_arc--cycle; % upper barb
  t:=xpart(lower_arc intersectiontimes(z9r--z8'));
  filldraw z8r--z9l--z9r--subpath(t,1) of lower_arc--cycle; % lower barb
 else: pos1(1.2flare,-100); pos8(1.2flare,-100);
  x1l=good.x(x1l+w-1.75u-rt x1); lft x8r=hround .8u;
  top y1l=vround .93h+o; bot y8r=vround .1h-o;
  filldraw stroke term.e(2,1,right,.9,4); % upper arc and terminal
  filldraw stroke term.e(7,8,left,1,4); fi % lower arc and terminal
 penlabels(0,1,1',2,3,4,5,6,7,8,8',9,10);
 hpos_corr=-u; put_accent;
repeat_once;
endchar;

ecchar "The letter S with hachek"; % from cshacheu.mf (csfonts)
beginchar(oct"222",10u#,cap_height#+acc_height#,0);
italcorr cap_height#*slant-u#;
adjust_fit(0,0);
numeric theta; theta=90-angle(50u,cap_height);
slope:=-cap_height/50u;  % angle at middle
numeric s_slab; s_slab=if serifs:slab else:vround .2[slab,cap_stem] fi;
numeric ess'; ess'=max(fine.breadth,cap_ess);
pickup fine.nib; pos2(max(fine.breadth,s_slab-vround vair_corr),-90);
pos0(ess',theta); pos7(s_slab,-90);
x2+x7=2x0=w; x7-x2=if serifs: u else: 0 fi;
top y2l=cap_height+o; bot y7r=-o;
y0=.52cap_height; lft x3l=hround u; rt x6r=hround(w-u);
x3r-x3l=x6r-x6l=hround .5[s_slab,ess']-fine;
ellipse_set(2l,3l,4l,0l); ellipse_set(2r,3r,4r,0r); y3=y3r;
ellipse_set(7l,6l,5l,0l); ellipse_set(7r,6r,5r,0r); y6=y6r;
filldraw stroke super_arc.e(2,3) & z3e{down}
 ..z4e---z5e..z6e{down} & super_arc.e(6,7);  % main stroke
if serifs: pos1(hair,180); pos8(hair,180);
 rt x1l=hround(w-1.5u); lft x8r=hround u;
 bot y1=vround 2/3cap_height+1; top y8=vround 1/3cap_height-1;
 filldraw stroke z1e{x2-x1,10(y2-y1)}....{left}z2e;  % upper arc
 filldraw stroke z7e{left}....{up}z8e;  % lower arc
 path upper_arc, lower_arc;
 upper_arc=z1{x2-x1,10(y2-y1)}....{left}z2;
 lower_arc=z7{left}....{up}z8;
 pos10(.3[fine.breadth,cap_hair],0); pos9(.3[fine.breadth,cap_hair],0);
 x10r=x1l; top y10=top y2l; x9l=x8r; bot y9=bot y7r;
 x1l-x1'=x8'-x8r=1.6cap_curve-fine; y1'=y1; y8'=y8;
 numeric t; t=xpart(upper_arc intersectiontimes(z10l--z1'));
 filldraw z1l--z10r--z10l--subpath(t,0) of upper_arc--cycle;  % upper barb
 t:=xpart(lower_arc intersectiontimes(z9r--z8'));
 filldraw z8r--z9l--z9r--subpath(t,1) of lower_arc--cycle;  % lower barb
else: pos1(1.2flare,-100); pos8(1.2flare,-100);
 x1l=good.x(x1l+w-1.75u-rt x1); lft x8r=hround .8u;
 top y1l=vround .93cap_height+o; bot y8r=vround .1cap_height-o;
 filldraw stroke term.e(2,1,right,.9,4);  % upper arc and terminal
 filldraw stroke term.e(7,8,left,1,4); fi  % lower arc and terminal
math_fit(0,.5ic#); penlabels(0,1,1',2,3,4,5,6,7,8,8',9,10); 
uc_hachek_accent(x0);   %(0.5[x3l,x1l]); oprava 24.10.92
endchar;

ecchar "The letter cedilla S";
beginchar(oct"223",10u#,cap_height#,0.875desc_depth#);
italcorr cap_height#*slant-u#;
adjust_fit(0,0);
numeric theta; theta=90-angle(50u,cap_height);
slope:=-cap_height/50u;  % angle at middle
numeric s_slab; s_slab=if serifs:slab else:vround .2[slab,cap_stem] fi;
numeric ess'; ess'=max(fine.breadth,cap_ess);
pickup fine.nib; pos2(max(fine.breadth,s_slab-vround vair_corr),-90);
pos0(ess',theta); pos7(s_slab,-90);
x2+x7=2x0=w; x7-x2=if serifs: u else: 0 fi;
top y2l=cap_height+o; bot y7r=-o;
y0=.52cap_height; lft x3l=hround u; rt x6r=hround(w-u);
x3r-x3l=x6r-x6l=hround .5[s_slab,ess']-fine;
ellipse_set(2l,3l,4l,0l); ellipse_set(2r,3r,4r,0r); y3=y3r;
ellipse_set(7l,6l,5l,0l); ellipse_set(7r,6r,5r,0r); y6=y6r;
filldraw stroke super_arc.e(2,3) & z3e{down}
 ..z4e---z5e..z6e{down} & super_arc.e(6,7);  % main stroke
if serifs: pos1(hair,180); pos8(hair,180);
 rt x1l=hround(w-1.5u); lft x8r=hround u;
 bot y1=vround 2/3cap_height+1; top y8=vround 1/3cap_height-1;
 filldraw stroke z1e{x2-x1,10(y2-y1)}....{left}z2e;  % upper arc
 filldraw stroke z7e{left}....{up}z8e;  % lower arc
 path upper_arc, lower_arc;
 upper_arc=z1{x2-x1,10(y2-y1)}....{left}z2;
 lower_arc=z7{left}....{up}z8;
 pos10(.3[fine.breadth,cap_hair],0); pos9(.3[fine.breadth,cap_hair],0);
 x10r=x1l; top y10=top y2l; x9l=x8r; bot y9=bot y7r;
 x1l-x1'=x8'-x8r=1.6cap_curve-fine; y1'=y1; y8'=y8;
 numeric t; t=xpart(upper_arc intersectiontimes(z10l--z1'));
 filldraw z1l--z10r--z10l--subpath(t,0) of upper_arc--cycle;  % upper barb
 t:=xpart(lower_arc intersectiontimes(z9r--z8'));
 filldraw z8r--z9l--z9r--subpath(t,1) of lower_arc--cycle;  % lower barb
else: pos1(1.2flare,-100); pos8(1.2flare,-100);
 x1l=good.x(x1l+w-1.75u-rt x1); lft x8r=hround .8u;
 top y1l=vround .93cap_height+o; bot y8r=vround .1cap_height-o;
 filldraw stroke term.e(2,1,right,.9,4);  % upper arc and terminal
 filldraw stroke term.e(7,8,left,1,4); fi  % lower arc and terminal
% the accent
uppercase_cedilla(x7,0,11,12,13,14,15);
math_fit(0,.5ic#);
penlabels(0,1,1',2,3,4,5,6,7,8,8',9,10,11,12,13,14,15); endchar;


ecchar "The letter T with hachek"; % from cshacheu.mf (csfonts)
beginchar(oct"224",13u#-width_adj#,cap_height#+acc_height#,0);
italcorr cap_height#*slant-beak_jut#-.25u#;
adjust_fit(0,0);
h':=h;
h:=vround(cap_height-2stem_corr);
pickup tiny.nib; pos1(cap_stem,0); pos2(cap_stem,0);
lft x1l=lft x2l=hround(.5w-.5cap_stem); top y1=h; bot y2=0;
filldraw stroke z1e--z2e; % stem
pickup crisp.nib; pos3(slab,90); pos4(hair,0);
top y3r=h; x3=x1; rt x4r=hround(w-.65u); y4=good.y(y3l-beak)-eps;
arm(3,4,e,beak_darkness,.7beak_jut);  % right arm and beak
pos5(hair,180); x5=w-x4; y5=y4;
arm(3,5,f,beak_darkness,-.7beak_jut);  % left arm and beak
if serifs: dish_serif(2,1,c,1/3,1.414cap_jut,d,1/3,1.414cap_jut);  % lower serif
 nodish_serif(1,2,a,1/3,.5cap_jut,b,1/3,.5cap_jut); fi  % upper bracketing
h:=h';
math_fit(-.75cap_height#*slant,ic#-2.5u#); penlabels(1,2,3,4,5,6); 
uc_hachek_accent(x1);
endchar;

ecchar "The letter cedilla T";
beginchar(oct"225",13u#-width_adj#,cap_height#,0.875desc_depth#);
italcorr cap_height#*slant-beak_jut#-.25u#;
adjust_fit(0,0);
h:=vround(cap_height-2stem_corr);
pickup tiny.nib; pos1(cap_stem,0); pos2(cap_stem,0);
lft x1l=lft x2l=hround(.5w-.5cap_stem); top y1=h; bot y2=0;
filldraw stroke z1e--z2e; % stem
pickup crisp.nib; pos3(slab,90); pos4(hair,0);
top y3r=h; x3=x1; rt x4r=hround(w-.65u); y4=good.y(y3l-beak)-eps;
arm(3,4,e,beak_darkness,.7beak_jut);  % right arm and beak
pos5(hair,180); x5=w-x4; y5=y4;
arm(3,5,f,beak_darkness,-.7beak_jut);  % left arm and beak
if serifs: dish_serif(2,1,c,1/3,1.414cap_jut,d,1/3,1.414cap_jut);  % lower serif
 nodish_serif(1,2,a,1/3,.5cap_jut,b,1/3,.5cap_jut); fi  % upper bracketing
% the accent
uppercase_cedilla(x2,hair,7,8,9,10,11);
math_fit(-.75cap_height#*slant,ic#-2.5u#);
penlabels(1,2,3,4,5,6,7,8,9,10,11); endchar;


ecchar "The letter U with double acute";
beginchar_twice(oct"226",13u#+.5width_adj#,cap_height#+acc_height#,0);
italcorr cap_height#*slant-cap_serif_fit#+cap_jut#-2.5u#+min(.5cap_stem#,u#);
if monospace: adjust_fit(cap_serif_fit#-.5u#,cap_serif_fit#-.5u#)
else: adjust_fit(cap_serif_fit#,cap_serif_fit#) fi;
pickup tiny.nib; pos1(cap_stem,0); pos2(cap_stem,0);
pos2'(cap_stem,180); z2'=z2;
pos3(cap_band,-90);
pos4(cap_hair,0); pos5(cap_hair,0);
x1=x2; x3=.5[x1,x5]; x4=x5; x1l=w-x5r;
top y1=top y5=cap_height; y2=y4=1/3cap_height; bot y3r=-o;
lft x1l=hround max(2u,3u-.5cap_stem);
filldraw stroke z1e--z2e; % left stem
filldraw stroke pulled_arc.e(2',3)
 & pulled_arc.e(3,4)&z4e--z5e; % arc and right stem
if serifs: dish_serif(1,2,a,1/3,cap_jut,b,1/3,cap_jut); % left serif
 dish_serif(5,4,c,1/2,cap_jut,d,1/2,cap_jut)(dark); fi  % right serif
math_fit(-cap_serif_fit#-.3cap_height#*slant-min(cap_height#*slant,u#),
 max(.5ic#-.5u#,0));
if monospace: hpos_corr=-2.5u; put_accent; hpos_corr=2.5u; put_accent;
else: hpos_corr=-1.5u; put_accent; hpos_corr=1.5u; put_accent; fi
repeat_once;
penlabels(1,2,3,4,5); endchar;

ecchar "The letter U with ring";
beginchar(oct"227",13u#+.5width_adj#,cap_height#+acc_height#,0);
italcorr cap_height#*slant-cap_serif_fit#+cap_jut#-2.5u#+min(.5cap_stem#,u#);
if monospace: adjust_fit(cap_serif_fit#-.5u#,cap_serif_fit#-.5u#)
else: adjust_fit(cap_serif_fit#,cap_serif_fit#) fi;
pickup tiny.nib; pos1(cap_stem,0); pos2(cap_stem,0);
pos2'(cap_stem,180); z2'=z2;
pos3(cap_band,-90);
pos4(cap_hair,0); pos5(cap_hair,0);
x1=x2; x3=.5[x1,x5]; x4=x5; x1l=w-x5r;
top y1=top y5=cap_height; y2=y4=1/3cap_height; bot y3r=-o;
lft x1l=hround max(2u,3u-.5cap_stem);
filldraw stroke z1e--z2e; % left stem
filldraw stroke pulled_arc.e(2',3)
 & pulled_arc.e(3,4)&z4e--z5e; % arc and right stem
if serifs: dish_serif(1,2,a,1/3,cap_jut,b,1/3,cap_jut); % left serif
 dish_serif(5,4,c,1/2,cap_jut,d,1/2,cap_jut)(dark); fi  % right serif
math_fit(-cap_serif_fit#-.3cap_height#*slant-min(cap_height#*slant,u#),
 max(.5ic#-.5u#,0));
% the accent
uppercase_circle(
if serifs: .5[x.b1,x.c1] else: x3 fi,
           cap_height+if hefty: .5 fi accent_gap,8,9,10,11);
penlabels(1,2,3,4,5,6,7,8,9,10,11); endchar;


ecchar "The letter umlaut Y";
beginchar(oct"230",13u#,cap_height#+acc_height#,0);
italcorr cap_height#*slant+.45u#;
adjust_fit(cap_serif_fit#,cap_serif_fit#);
numeric left_stem,right_stem,outer_jut,dy,alpha;
left_stem=cap_stem-stem_corr;
right_stem=min(cap_hair if hefty:-2stem_corr fi,left_stem);
outer_jut=.75cap_jut; x1l=w-x4r=l+letter_fit+outer_jut+.05u;
x2l=x5l=hround(.5w-.5left_stem); x3r=x5r;
y1=y4=cap_height; y2=y3=y5=.4cap_height;
dy=y1-y2; alpha=((x2l-x1l)++dy)/dy;
penpos1(alpha*left_stem,0); penpos2(alpha*left_stem,0);
penpos3(alpha*right_stem,0); penpos4(alpha*right_stem,0);
penpos5(left_stem,0); penpos6(left_stem,0); x5=x6; y6=0;
z0=whatever[z1r,z2r]=whatever[z3l,z4l];
if y0>y2+cap_notch_cut: y0:=y2+cap_notch_cut;
  fill z0+.5right{up}...{z4-z3}diag_end(3l,4l,1,1,4r,3r)--z5r
    --diag_end(5r,6r,1,1,6l,5l)--z5l--diag_end(2l,1l,1,1,1r,2r){z2-z1}
    ...{down}z0+.5left--cycle; % diagonals and stem
else: fill z0--diag_end(0,4l,1,1,4r,3r)--z5r
    --diag_end(5r,6r,1,1,6l,5l)--z5l
    --diag_end(2l,1l,1,1,1r,0)--cycle; fi  % diagonals and stem
if serifs: numeric inner_jut; pickup tiny.nib;
 prime_points_inside(1,2); prime_points_inside(4,3); prime_points_inside(6,5);
 if rt x1'r+cap_jut+.5u+1<=lft x4'l-cap_jut: inner_jut=cap_jut;
 else: rt x1'r+inner_jut+.5u+1=lft x4'l-inner_jut; fi
 dish_serif(1',2,a,1/3,outer_jut,b,1/2,inner_jut);  % upper left serif
 dish_serif(4',3,c,.6,inner_jut,d,1/2,outer_jut)(dark);  % upper right serif
 dish_serif(6',5,e,1/3,cap_jut,f,1/3,cap_jut); fi  % lower serif
% the accent
uppercase_umlaut(0,0,7,8,9,10);
math_fit(.5u#-cap_height#*slant,ic#-4u#);
penlabels(0,1,2,3,4,5,6,7,8,9,10); endchar;

ecchar "The letter Z with acute"; % from pl_dl (plfonts)
beginchar_twice(oct"231",11u#,cap_height#,0);
 italcorr cap_height#*slant-.5u#;
 adjust_fit(0,0);
 numeric arm_thickness[\\],z_stem;
 if hefty: arm_thickness1=Vround(slab-vair_corr); arm_thickness2=slab;
  z_stem=.8[vair,cap_stem];
 else: arm_thickness1=slab; arm_thickness2=vround(slab+vair_corr);
  z_stem=.9[vair,cap_stem]; fi
 pickup tiny.nib; x3l=x4l=w-x1r=w-x2r; lft x3l=hround u;
 top y1=h; y2=min(y1,h-2/3arm_thickness1);
 bot y4=0; y3=max(y4,2/3arm_thickness2);
 numeric alpha; alpha=diag_ratio(1,z_stem-tiny,y2-y3,x2r-x3l);
 penpos1(alpha*(z_stem-tiny),0); penpos2(alpha*(z_stem-tiny),0);
 penpos3(alpha*(z_stem-tiny),0); penpos4(alpha*(z_stem-tiny),0);
 pair delta; delta=penoffset z3-z2 of currentpen;
 fill top lft z1l--z2l+delta---z3l+delta..lft z3l---lft z4l..bot z4l
  ---bot rt z4r--z3r-delta---z2r-delta..rt z2r---rt z1r..top z1r
  ---cycle; % diagonal
 pickup crisp.nib; pos5(arm_thickness1,90); pos6(hair,180);
 top y5r=h; x5=x1; lft x6r=hround 1.25u; y6=good.y(y5l-beak)-eps;
 arm(5,6,a,beak_darkness**.8,-.4beak_jut); % upper arm and beak
 pos7(arm_thickness2,-90); pos8(hair,0);
 bot y7r=0; x7=x4; rt x8r=hround(w-.9u); y8=good.y(y7l+1.2beak)+eps;
 arm(7,8,b,beak_darkness**.9,.6beak_jut); % lower arm and beak
 penlabels(1,2,3,4,5,6,7,8);
 hpos_corr=-.5u; put_accent;
repeat_once;
endchar;

ecchar "The letter Z with hachek"; % from cshacheu.mf (csfonts)
beginchar(oct"232",11u#,cap_height#+acc_height#,0);
italcorr cap_height#*slant-.5u#;
adjust_fit(0,0);
numeric arm_thickness[],z_stem;
if hefty: arm_thickness1=Vround(slab-vair_corr); arm_thickness2=slab;
 z_stem=.8[vair,cap_stem];
else: arm_thickness1=slab; arm_thickness2=vround(slab+vair_corr);
 z_stem=.9[vair,cap_stem]; fi
pickup tiny.nib; x3l=x4l=w-x1r=w-x2r; lft x3l=hround u;
top y1=cap_height; y2=min(y1,cap_height-2/3arm_thickness1);
bot y4=0; y3=max(y4,2/3arm_thickness2);
numeric alpha; alpha=diag_ratio(1,z_stem-tiny,y2-y3,x2r-x3l);
penpos1(alpha*(z_stem-tiny),0); penpos2(alpha*(z_stem-tiny),0);
penpos3(alpha*(z_stem-tiny),0); penpos4(alpha*(z_stem-tiny),0);
pair delta; delta=penoffset z3-z2 of currentpen;
fill top lft z1l--z2l+delta---z3l+delta..lft z3l---lft z4l..bot z4l
 ---bot rt z4r--z3r-delta---z2r-delta..rt z2r---rt z1r..top z1r
 ---cycle;  % diagonal
pickup crisp.nib; pos5(arm_thickness1,90); pos6(hair,180);
top y5r=cap_height; x5=x1; lft x6r=hround 1.25u; y6=good.y(y5l-beak)-eps;
arm(5,6,a,beak_darkness**.8,-.4beak_jut);  % upper arm and beak
pos7(arm_thickness2,-90); pos8(hair,0);
bot y7r=0; x7=x4; rt x8r=hround(w-.9u); y8=good.y(y7l+1.2beak)+eps;
arm(7,8,b,beak_darkness**.9,.6beak_jut);  % lower arm and beak
math_fit(0,.5ic#); penlabels(1,2,3,4,5,6,7,8); 
uc_hachek_accent(1/2[x6,x1]);
endchar;


ecchar "The letter Z with dot above"; % from pl_dl (plfonts)
beginchar_twice(oct"233",11u#,cap_height#,0);
 italcorr cap_height#*slant-.5u#;
 adjust_fit(0,0);
 numeric arm_thickness[\\],z_stem;
 if hefty: arm_thickness1=Vround(slab-vair_corr); arm_thickness2=slab;
  z_stem=.8[vair,cap_stem];
 else: arm_thickness1=slab; arm_thickness2=vround(slab+vair_corr);
  z_stem=.9[vair,cap_stem]; fi
 pickup tiny.nib; x3l=x4l=w-x1r=w-x2r; lft x3l=hround u;
 top y1=h; y2=min(y1,h-2/3arm_thickness1);
 bot y4=0; y3=max(y4,2/3arm_thickness2);
 numeric alpha; alpha=diag_ratio(1,z_stem-tiny,y2-y3,x2r-x3l);
 penpos1(alpha*(z_stem-tiny),0); penpos2(alpha*(z_stem-tiny),0);
 penpos3(alpha*(z_stem-tiny),0); penpos4(alpha*(z_stem-tiny),0);
 pair delta; delta=penoffset z3-z2 of currentpen;
 fill top lft z1l--z2l+delta---z3l+delta..lft z3l---lft z4l..bot z4l
  ---bot rt z4r--z3r-delta---z2r-delta..rt z2r---rt z1r..top z1r
  ---cycle; % diagonal
 pickup crisp.nib; pos5(arm_thickness1,90); pos6(hair,180);
 top y5r=h; x5=x1; lft x6r=hround 1.25u; y6=good.y(y5l-beak)-eps;
 arm(5,6,a,beak_darkness**.8,-.4beak_jut); % upper arm and beak
 pos7(arm_thickness2,-90); pos8(hair,0);
 bot y7r=0; x7=x4; rt x8r=hround(w-.9u); y8=good.y(y7l+1.2beak)+eps;
 arm(7,8,b,beak_darkness**.9,.6beak_jut); % lower arm and beak
 penlabels(1,2,3,4,5,6,7,8);
 put_dot(tiny,max(dot_size#,cap_curve#));
repeat_once;
endchar;

ecchar "The letter IJ";
beginchar(oct"234",
  9u#+max(6u#,4u#+cap_stem#)
  if not serifs: -u# fi,cap_height#,0);
italcorr cap_height#*slant-cap_serif_fit#
 +.75cap_jut#-2.5u#+min(.5cap_stem#,u#);
adjust_fit(cap_serif_fit#,cap_serif_fit#);
pickup tiny.nib; pos1(cap_stem,0); pos2(cap_stem,0);
lft x1l=lft x2l=hround(.5(w-9u)-.5cap_stem); top y1=h; bot y2=0;
filldraw stroke z1e--z2e; % stem
if serifs: dish_serif(1,2,a,1/3,1.05cap_jut,b,1/3,1.05cap_jut);  % upper serif
 dish_serif(2,1,c,1/3,1.05cap_jut,d,1/3,1.05cap_jut);  fi % lower serif
% J part
pickup tiny.nib; pos11(cap_stem',0); pos12(cap_stem',0);
top y11=h; rt x11r=hround(w-2u); x12=x11; y12=.21h;
if serifs: pos13(vair,-90); pos14(cap_hair,-180);
 pos15(flare+(cap_stem-stem),-180);
 bot y13r=-o; x13=.5[x14,x12]; y15=1/6h; rt x15l=hround w-9u+2.75u;
 z15r=z14r;
 filldraw stroke z11e--z12e&super_arc.e(12,13);  % stem and arc
 dish_serif(11,12,aa,1/3,1.3cap_jut,bb,1/3,.75cap_jut);  % serif
 pickup tiny.nib; bulb(13,14,15);  % bulb
else: filldraw stroke z11e--z12e;  % stem
 pickup fine.nib; pos13(cap_stem',0); z13=z12;
 pos14(flare,angle(6.5u,-h)); pos15(1.1flare,-100);
 bot y14r=-o;
 x14r=.5[x15r,x13r];
 lft x15r=hround max(6u,4u+cap_stem)-0.25u;% +.75u;
 bot y15r=vround.06h-o;
 filldraw stroke z13e{down}....term.e(14,15,left,1,4); fi % arc and terminal
math_fit(0,.5ic#-.5u#);
penlabels(1,2,3,4,5,11,12,13,14,15); endchar;

iff not suppress_i_dot:
ecchar "The letter I with dot";
beginchar_twice(oct"235",max(6u#,4u#+cap_stem#),cap_height#,0);
italcorr cap_height#*slant-.25u#;
adjust_fit(cap_serif_fit#,cap_serif_fit#);
pickup tiny.nib; pos1(cap_stem,0); pos2(cap_stem,0);
lft x1l=lft x2l=hround(.5w-.5cap_stem); top y1=cap_height; bot y2=0;
filldraw stroke z1e--z2e; % stem
if serifs or monospace:
 dish_serif(1,2,a,1/3,1.05cap_jut,b,1/3,1.05cap_jut);  % upper serif
 dish_serif(2,1,c,1/3,1.05cap_jut,d,1/3,1.05cap_jut);  fi % lower serif
if slitex:
 pickup crisp.nib;           % arms based on proportions of uppercase E
 pos3(slab,90); pos4(slab if not serifs:+2stem_corr fi,-90);
 x3=x4=x1; top y3r=h; bot y4r=0;
 pos5(hair,0); pos6(hair,0); % top and bottom right arms
 rt x5r=rt x6r=hround(w-u);
 pos7(hair,180); pos8(hair,180); % top and bottom left arms
 lft x7r=lft x8r=hround(u);
 y5=y7=good.y(y3l-beak)-eps; % beak in case we ever want a beaked serif!
 y6=y8=good.y(y4l+beak)+eps; % watch that this doesn't get too heavy
 arm(3,5,a,darkness,jut); arm(3,7,b,darkness,jut); % top left and right arms
 arm(4,6,c,darkness,jut); arm(4,8,d,darkness,jut); % bottom left and right
fi
put_dot(tiny,max(dot_size#,cap_curve#));
repeat_once;
math_fit(0,.5ic#); penlabels(1,2,3,4,5,6); endchar;

endinput;
