% exrleast.mf
%
% (c) Copyright 1995, 1996, 1997 J"org Knappen
% (c) Copyright 1990, 1992 Norbert Schwarz
%
% This file is part of ecfonts version 1.0
%
% Please read the files 00readme.txt, 00inst.txt, 00error.txt, and
% copyrite.txt for further information
%
% You find some documentation in ecdoc.tex (needs LaTeX2e)
%
% Content:
%
%  accented lowercase letters ("eastern part")
%
%   oct"236", oct"240" .. oct"274"
%
%       oct"236"    d  with bar
%
%       oct"240"    a  with breve
%       oct"241"    a  with ogonek
%       oct"242"    c  with acute
%       oct"243"    c  with hacek
%       oct"244"    d  with comma
%       oct"245"    e  with hacek
%       oct"246"    e  with ogonek
%       oct"247"    g  with breve
%
%       oct"250"    l  with acute
%       oct"251"    l  with komma
%       oct"252"    l  with (polnisches L)
%       oct"253"    n  with acute
%       oct"254"    n  with hacek
%       oct"255"    the letter eng
%       oct"256"    o  with ungarian umlauts
%       oct"257"    r  with acute
%
%       oct"260"    r  with hacek
%       oct"261"    s  with acute
%       oct"262"    s  with hacek
%       oct"263"    s  with cedille
%       oct"264"    t  with comma
%       oct"265"    t  with cedille
%       oct"266"    u  with hungarian umlauts
%       oct"267"    u  with circ
%
%       oct"270"    y with  umlauts
%       oct"271"    z with  acute
%       oct"272"    z with  hacek
%       oct"273"    z with  dot
%       oct"274"    ij

version_check(1,0);  % |version_check| was introduced in dc1.3

ecchar "The letter bar d";
beginchar(oct"236",10u#+serif_fit#,asc_height#,0);
italcorr asc_height#*slant-serif_fit#+.5stem#-2u#;
adjust_fit(0,serif_fit#);
pickup tiny.nib; pos1(stem',0); pos2(stem,0);
pos0'(stem',0); pos0(stem,0); z0r=z0'r; x0'=x1; x0=x2;
rt x1r=hround(w-2.5u+.5stem'); top y1=h;
numeric edge; edge=lft x2l;
pickup fine.nib; pos3(if hefty:thin_join else: hair fi,0);
pos4(vair,90); pos5(curve,180); pos6(vair,270); penpos7(x3r-x3l,360);
lft x3l=min(lft x3l-(rt x3r-tiny.rt x2r),1/3[lft x2,edge]);
y3=1/8[bar_height,x_height];
x4l=.5(w-serif_fit)-.3u; top y4r=x_height+oo if monospace: -hair fi;
lft x5r=hround max(1.35u-.5curve,.6u); y5=.5x_height if monospace: -.5hair fi ;
x6l=x4l-.2u; bot y6r=-oo;
x7=x3; y7=min(y3,y6+y4-y3+.6vair);
(x,y4r)=whatever[z3l,z4l]; x4r:=max(x,.5[x5r,x4]);
(x',y6r)=whatever[z7l,z6l]; x6r:=max(x',.5[x5r,x6]);
filldraw stroke z3e{up}...pulled_arc.e(4,5)
 & pulled_arc.e(5,6)...{up}z7e; % bowl
y0=ypart(((edge,h)--(edge,0))intersectionpoint(z3l{up}...{left}z4l));
pickup tiny.nib; bot y2=if serifs:-min(oo,serif_drop) else: 0 fi;
filldraw stroke z1e--z0'e--z0e--z2e;  % stem
if serifs: sloped_serif.l(1,0',a,1/3,jut,serif_drop);  % upper serif
 sloped_serif.r(2,0,b,1/3,jut,min(oo,serif_drop)); fi  % lower serif
%
  pos8(bar,90); pos9(bar,90);
  x8=.5[x4,x5];
  if serifs: x9=x2r+jut; else: x9=w; fi
  if serifs: y8=y9=.5[y.a2-.5slab,y4+.5vair];
  else: y8=y9=.5[x_height,asc_height-serif_drop]; fi
  filldraw stroke z8e--z9e;
penlabels(0,1,2,3,4,5,6,7,8,9); endchar;



%%%%%%


ecchar "The letter a with breve";
beginchar(oct"240",9u#,x_height#+acc_height#,0);
bh#:=min(bar_height#,1.14x_height#-bar_height#); define_pixels(bh);
italcorr 1/3[bh#,x_height#]*slant+.5stem#-serif_fit#-2u#;
adjust_fit(0,serif_fit# if serifs: if hair#+.5stem#>1.5u#:-.25u# fi\\fi);
pickup fine.nib; top y3r=x_height+vround 1.5oo;
if serifs: pos1(flare,180); pos2(hair,180);
 pos3(vair,90);  lft x1r=hround max(u,2.1u-.5flare); x3=.5w-.5u;
 y1=min(bh+.5flare+2vair+2,.9[bh,x_height]-.5flare);
 bulb(3,2,1);  % bulb
else: pos1(5/7[vair,flare],95); x1l=good.x 1.5u; x1r:=good.x x1r;
 pos3(1/8[vair,thin_join],90);
 x3=.5w-.2u; top y1r=vround .82[bh,top y3r];
 filldraw stroke term.e(3,1,left,.9,4); fi  % terminal
pos4(stem,0); rt x4r=hround(w-2.5u+.5stem); y4=1/3[bh,x_height];
pos5(stem,0); x5=x4; y5=max(.55bh,2vair);
filldraw stroke super_arc.e(3,4)&z4e..z5e;  % arc and stem
pos6(.3[thin_join,vair],90); x6=x4; bot y6=bh;
pos7(hround(curve-2stem_corr),180);
lft x7r=hround max(.5u,1.5u-.5curve); y7=1/3[top y8l,top y6r];
pos8(vair,270); x8l=.5w-.75u; bot y8r=-oo;
pos9(thin_join,360); z9l=z5l;
(x,y8r)=whatever[z8l,z9l]; x8r:=max(x,x8-u);
{{interim superness:=more_super;
 filldraw stroke z9e{down}...z8e{left}...{up}z7e&super_arc.e(7,6)}}; % bowl
if serifs: numeric shaved_stem; shaved_stem=hround(stem-3stem_corr);
 if hair#+.5stem#>1.5u#: pickup tiny.nib;
  pos5'(shaved_stem,0); rt x5'r=fine.rt x5r; y5'=y5;
  pos10(shaved_stem,0); x10=x5'; y10=.2[.5tiny,bh];
  pos11(shaved_stem,0); rt x11r=hround(w-.25u); bot y11=0;
  pos12(shaved_stem,0); x11=x12; top y12=slab+eps;
  filldraw z5'l---z10l...z11l{right}--z11r
   --z12r{left}...z10r+.75(z12-z11)---z5'r--cycle;  % foot
 else: pickup crisp.nib; pos5'(shaved_stem,0); rt x5'r=fine.rt x5r; y5'=y5;
  pos10(shaved_stem,0); x10=x5'; y10=1/3bh;
  pos11(.2[vair,stem],90); x11r=.5[x10r,x12r]; bot y11l=-vround .5oo;
  pos12(hair,180); rt x12l=hround(w-.1u); y12=max(y10,y11+vair);
  pos13(hair,180); x13=x12; top y13=max(vround .6bh,top y12);
  (x',y11l)=whatever[z11r,z12r]; x11l:=max(x',x10);
  filldraw stroke z5'e---z10e...z11e{right}...z12e---z13e; fi  % hook
else: numeric shaved_stem; shaved_stem=hround(stem-stem_corr);
 pickup tiny.nib; pos5'(shaved_stem,0); rt x5'r=fine.rt x5r; y5'=y5;
 pos10(shaved_stem,0); x10=x5'; bot y10=0;
 filldraw stroke z5'e--z10e; fi  % base of stem
% the accent
lowercase_breve(.5w,0,14,15,16);
penlabels(1,2,3,4,5,6,7,8,9,10,11,12,13,14,15,16); endchar;

ecchar "The letter a with ogonek"; % from pl_ml (plfonts)
beginchar(oct"241",9u#,x_height#,the_ogonek_depth#);
 bh#:=min(bar_height#,1.14x_height#-bar_height#); define_pixels(bh);
 italcorr 1/3[bh#,x_height#]*slant+.5stem#-serif_fit#-2u#;
 adjust_fit(0,serif_fit# if serifs: if hair#+.5stem#>1.5u#:-.25u# fi\\fi);
 pickup fine.nib; top y3r=h+vround 1.5oo;
 if serifs: pos1(flare,180); pos2(hair,180);
  pos3(vair,90);  lft x1r=hround max(u,2.1u-.5flare); x3=.5w-.5u;
  y1=min(bh+.5flare+2vair+2,.9[bh,h]-.5flare);
  bulb(3,2,1);  % bulb
 else: pos1(5/7[vair,flare],95); x1l=good.x 1.5u; x1r:=good.x x1r;
  pos3(1/8[vair,thin_join],90);
  x3=.5w-.2u; top y1r=vround .82[bh,top y3r];
  filldraw stroke term.e(3,1,left,.9,4); fi  % terminal
 pos4(stem,0); rt x4r=hround(w-2.5u+.5stem); y4=1/3[bh,h];
 pos5(stem,0); x5=x4; y5=max(.55bh,2vair);
 filldraw stroke super_arc.e(3,4)&z4e..z5e;  % arc and stem
 pos6(.3[thin_join,vair],90); x6=x4; bot y6=bh;
 pos7(hround(curve-2stem_corr),180);
 lft x7r=hround max(.5u,1.5u-.5curve); y7=1/3[top y8l,top y6r];
 pos8(vair,270); x8l=.5w-.75u; bot y8r=-oo;
 pos9(thin_join,360); z9l=z5l;
 (x,y8r)=whatever[z8l,z9l]; x8r:=max(x,x8-u);
 {{interim superness:=more_super;
  filldraw stroke z9e{down}...z8e{left}...{up}z7e&super_arc.e(7,6)}}; % bowl
 if serifs: numeric shaved_stem; shaved_stem=hround(stem-3stem_corr);
  if is_bold: pickup tiny.nib;
   pos5'(shaved_stem,0); rt x5'r=fine.rt x5r; y5'=y5;
   pos10(shaved_stem,0); x10=x5'; y10=.2[.5tiny,bh];
   pos11(shaved_stem,0); rt x11r=hround(w-.25u); bot y11=0;
   pos12(shaved_stem,0); x11=x12; top y12=slab+eps;
   filldraw z5'l---z10l...z11l{right}--z11r
    --z12r{left}...z10r+.75(z12-z11)---z5'r--cycle;  % foot
  else: pickup crisp.nib; pos5'(shaved_stem,0); rt x5'r=fine.rt x5r; y5'=y5;
   pos10(shaved_stem,0); x10=x5'; y10=1/3bh;
   pos11(.2[vair,stem],90); x11r=.5[x10r,x12r]; bot y11l=-vround .5oo;
   pos12(hair,180); rt x12l=hround(w-.1u); y12=max(y10,y11+vair);
   pos13(hair,180); x13=x12; top y13=max(vround .6bh,top y12);
   (x',y11l)=whatever[z11r,z12r]; x11l:=max(x',x10);
   filldraw stroke z5'e---z10e...z11e{right}...z12e---z13e; fi  % hook
 else: numeric shaved_stem; shaved_stem=hround(stem-stem_corr);
  pickup tiny.nib; pos5'(shaved_stem,0); rt x5'r=fine.rt x5r; y5'=y5;
  pos10(shaved_stem,0); x10=x5'; bot y10=0;
  filldraw stroke z5'e--z10e; % base of stem
 fi;
 penlabels(1,2,3,4,5,6,7,8,9,10,11,12,13);
 if serifs: path p.l; p.l=z5'l---z10l...z11l{right}; fi
 def the_pen=
  if known ogonek_pen: ogonek_pen elseif serifs: tiny else: fine fi
 enddef;
 ogonek_breadth=if serifs and is_bold: slab else: xvair fi;
 ogonek_pos=if serifs: (point if is_bold: 1.75 of p.l) else: 1.85 of p.l)+
  .5the_pen*up fi\\ else: (x10l,.5the_pen) fi;
 put_ogonek(the_pen);
endchar;

ecchar "The letter c with acute"; % from pl_ml (plfonts) 
beginchar_twice(oct"242",8u#,x_height#,0);
 italcorr x_height#*slant-.2u#;
 adjust_fit(if monospace: .5u#,.5u# else: 0,0 fi);
 pickup fine.nib; pos2(vair',90); pos4(vair',270);
 x2=x4=.5(w+u); top y2r=vround(h+1.5oo); bot y4r=-oo;
 pos3(curve,180); lft x3r=hround max(.6u,1.35u-.5curve); y3=.5h;
 if serifs: pos1(hair,0); pos0(flare,0);
  y1=min(bar_height+.5flare+2vair'+2,.9[bar_height,h]-.5flare);
  rt x1r=hround(w-.7u); bulb(2,1,0);  % bulb
  pos5(hair,0); rt x5r=hround(w-.5u);
  y5=max(good.y(.5bar_height-.9),y4l+vair');
  (x,y4l)=whatever[z4r,z5l]; x4l:=min(x,x4l+.5u);
   filldraw stroke pulled_super_arc.e(2,3)(.7superpull)
   & pulled_super_arc.e(3,4)(.5superpull)
   ..tension .9 and 1..{x5-x4,5(y5-y4)}z5e;  % arc and lower terminal
 else: pos1(4/7[vair',flare],80);
  rt x1r=hround(w-.6u); top y1r=vround .82[bar_height,top y2r];
  filldraw stroke term.e(2,1,right,.8,4);  % upper terminal
  pos5(.6[vair',flare],275); rt x5r=hround(w-.5u);
  y5r=good.y(y5r+1/3bar_height-y5); y5l:=good.y y5l; x5l:=good.x x5l;
  forsuffixes e=l,r: path p.e; p.e=z4e{right}..tension .9 and 1..z5e;
   if angle direction 1 of p.e>75:
    p.e:=z4e{right}..tension atleast.9 and 1..{dir 75}z5e; fi endfor
  filldraw stroke pulled_super_arc.e(2,3)(.7superpull)
   & pulled_super_arc.e(3,4)(.5superpull) & p.e; fi  % arc and lower terminal
 penlabels(0,1,2,3,4,5);
 hpos_corr=-.25u; put_accent;
repeat_once;
endchar;

ecchar "The letter c with hachek"; % from cshachel.mf (csfonts)
beginchar(oct"243",8u#,lc_hachek_height#,0);
italcorr x_height#*slant-.2u#;
adjust_fit(if monospace: .5u#,.5u# else: 0,0 fi);
pickup fine.nib; pos2(vair',90); pos4(vair',270);
x2=x4=.5(w+u); top y2r=vround(x_height+1.5oo); bot y4r=-oo;
pos3(curve,180); lft x3r=hround max(.6u,1.35u-.5curve); y3=.5x_height;
if serifs: pos1(hair,0); pos0(flare,0);
 y1=min(bar_height+.5flare+2vair'+2,.9[bar_height,x_height]-.5flare);
 rt x1r=hround(w-.7u); bulb(2,1,0);  % bulb
 pos5(hair,0); rt x5r=hround(w-.5u);
 y5=max(good.y(.5bar_height-.9),y4l+vair');
 (x,y4l)=whatever[z4r,z5l]; x4l:=min(x,x4l+.5u);
 filldraw stroke pulled_super_arc.e(2,3)(.7superpull)
  & pulled_super_arc.e(3,4)(.5superpull)
  ..tension .9 and 1..{x5-x4,5(y5-y4)}z5e;  % arc and lower terminal
else: pos1(4/7[vair',flare],80);
 rt x1r=hround(w-.6u); top y1r=vround .82[bar_height,top y2r];
 filldraw stroke term.e(2,1,right,.8,4);  % upper terminal
 pos5(.6[vair',flare],275); rt x5r=hround(w-.5u);
 y5r=good.y(y5r+1/3bar_height-y5); y5l:=good.y y5l; x5l:=good.x x5l;
 forsuffixes e=l,r: path p.e; p.e=z4e{right}..tension .9 and 1..z5e;
  if angle direction 1 of p.e>75:
   p.e:=z4e{right}..tension atleast.9 and 1..{dir 75}z5e; fi endfor
 filldraw stroke pulled_super_arc.e(2,3)(.7superpull)
  & pulled_super_arc.e(3,4)(.5superpull) & p.e; fi  % arc and lower terminal
penlabels(0,1,2,3,4,5);
lc_hachek_accent(if serifs: 0.5[0.5[x3l,x3],x0r] else: x2 fi);
endchar;   %% changed from ..[x3r,x3]..


ecchar "The letter d with hachek"; % from csotherl.mf (csfonts)
beginchar(oct"244",10u#+serif_fit#,lc_acute_height#,0);
italcorr lc_acute_height#*slant-serif_fit#+.5stem#-2u#;
adjust_fit(0, if monospace: 2u# else: dot_size#+0.25u# fi);
pickup tiny.nib; pos1(stem',0); pos2(stem,0);
pos0'(stem',0); pos0(stem,0); z0r=z0'r; x0'=x1; x0=x2;
rt x1r=hround(w-2.5u+.5stem'); top y1=h;
numeric edge; edge=lft x2l;
pickup fine.nib; pos3(if hefty:thin_join else: hair fi,0);
pos4(vair,90); pos5(curve,180); pos6(vair,270); penpos7(x3r-x3l,360);
lft x3l=min(lft x3l-(rt x3r-tiny.rt x2r),1/3[lft x2,edge]);
y3=1/8[bar_height,x_height];
x4l=.5(w-serif_fit)-.3u; top y4r=x_height+oo;
lft x5r=hround max(1.35u-.5curve,.6u); y5=.5x_height;
x6l=x4l-.2u; bot y6r=-oo;
x7=x3; y7=min(y3,y6+y4-y3+.6vair);
(x,y4r)=whatever[z3l,z4l]; x4r:=max(x,.5[x5r,x4]);
(x',y6r)=whatever[z7l,z6l]; x6r:=max(x',.5[x5r,x6]);
filldraw stroke z3e{up}...pulled_arc.e(4,5)
 & pulled_arc.e(5,6)...{up}z7e; % bowl
y0=ypart(((edge,h)--(edge,0))intersectionpoint(z3l{up}...{left}z4l));
pickup tiny.nib; bot y2=if serifs:-min(oo,serif_drop) else: 0 fi;
filldraw stroke z1e--z0'e--z0e--z2e;  % stem
if serifs: sloped_serif.l(1,0',a,1/3,jut,serif_drop);  % upper serif
 sloped_serif.r(2,0,b,1/3,jut,min(oo,serif_drop)); fi  % lower serif
penlabels(0,1,2,3,4,5,6,7); 
dtl_hachek(x0r+u+.5pdot_diam)(y1);
endchar;

ecchar "The letter e with hachek"; % from cshachel.mf (csfonts)
beginchar(oct"245",7.25u#+max(.75u#,.5curve#),lc_hachek_height#,0);
italcorr .5[bar_height#,x_height#]*slant+.5min(curve#-1.5u#,0);
adjust_fit(if monospace: .25u#,.5u# else: 0,0 fi);
numeric left_curve,right_curve;
left_curve=right_curve+6stem_corr=curve if not serifs: -3stem_corr fi;
if right_curve<tiny.breadth: right_curve:=tiny.breadth; fi
if left_curve<tiny.breadth: left_curve:=tiny.breadth; fi
pickup tiny.nib; pos1(right_curve,0);
pos2(vair,90); pos3(left_curve,180);
y1=good.y bar_height; top y2r=x_height+vround 1.5oo; y0l=bot y1;
rt x1r=hround min(w-.5u,w-u+.5right_curve);
lft x3r=hround max(.5u,1.25u-.5left_curve); x2=.5w+.25u;
{{interim superness:=more_super;
 filldraw stroke super_arc.e(1,2)}};  % right bowl
y3=.5[y2,y4]; bot y4r=-oo; x4=x2+.25u;
if serifs: pos4(vair',270); pos5(hair,360);
 y5=max(good.y(.5bar_height-.9),y4l+vair); x5r=x1r;
 (x,y4l)=whatever[z4r,z5]; x4l:=min(x,x4l+.5u);
 filldraw stroke pulled_arc.e(2,3) & pulled_arc.e(3,4)
  ...{x5-x4,5(y5-y4)}z5e; % left bowl, arc, and terminal
else: pos4(vair,270);
 filldraw stroke super_arc.e(2,3) & super_arc.e(3,4);  % left bowl and arc
 pickup fine.nib; pos4'(vair,270); z4=z4';
 pos5(.5[vair,flare],275); rt x5r=hround(w-.6u);
 y5r=good.y(y5r+1/3bar_height-y5); y5l:=good.y y5l; x5l:=good.x x5l;
 filldraw stroke term.e(4',5,right,1,4); fi  % terminal
path testpath; testpath=super_arc.r(2,3) & super_arc.r(3,4);
y1'r=y0r=y0l+.6[thin_join,vair]; y1'l=y0l; x1'l=x1'r=x1;
forsuffixes $=l,r:
 x0$=xpart(((0,y0$)--(x1,y0$)) intersectionpoint testpath); endfor
fill stroke z0e--z1'e;  % crossbar
penlabels(0,1,2,3,4,5);
lc_hachek_accent(x2);
endchar;


ecchar "The letter e with ogonek"; % from pl_ml (plfonts)
beginchar(oct"246",7.25u#+max(.75u#,.5curve#),x_height#,the_ogonek_depth#);
 italcorr .5[bar_height#,x_height#]*slant+.5min(curve#-1.5u#,0);
 adjust_fit(if monospace: .25u#,.5u# else: 0,0 fi);
 numeric left_curve,right_curve;
 left_curve=right_curve+6stem_corr=curve if not serifs: -3stem_corr fi;
 if right_curve<tiny.breadth: right_curve:=tiny.breadth; fi
 if left_curve<tiny.breadth: left_curve:=tiny.breadth; fi
 pickup tiny.nib; pos1(right_curve,0);
 pos2(vair,90); pos3(left_curve,180);
 y1=good.y bar_height; top y2r=h+vround 1.5oo; y0l=bot y1;
 rt x1r=hround min(w-.5u,w-u+.5right_curve);
 lft x3r=hround max(.5u,1.25u-.5left_curve); x2=.5w+.25u;
 {{interim superness:=more_super;
  filldraw stroke super_arc.e(1,2)}};  % right bowl
 y3=.5[y2,y4]; bot y4r=-oo; x4=x2+.25u;
 if serifs: pos4(vair',270); pos5(hair,360);
  y5=max(good.y(.5bar_height-.9),y4l+vair); x5r=x1r;
  (x,y4l)=whatever[z4r,z5]; x4l:=min(x,x4l+.5u);
  filldraw stroke pulled_arc.e(2,3) & pulled_arc.e(3,4)
   ...{x5-x4,5(y5-y4)}z5e; % left bowl, arc, and terminal
 else: pos4(vair,270);
  filldraw stroke super_arc.e(2,3) & super_arc.e(3,4);  % left bowl and arc
  pickup fine.nib; pos4'(vair,270); z4=z4';
  pos5(.5[vair,flare],275); rt x5r=hround(w-.6u);
  y5r=good.y(y5r+1/3bar_height-y5); y5l:=good.y y5l; x5l:=good.x x5l;
  filldraw stroke term.e(4',5,right,1,4); fi  % terminal
 path testpath; testpath=super_arc.r(2,3) & super_arc.r(3,4);
 y1'r=y0r=y0l+.6[thin_join,vair]; y1'l=y0l; x1'l=x1'r=x1;
 forsuffixes $=l,r:
  x0$=xpart(((0,y0$)--(x1,y0$)) intersectionpoint testpath); endfor
 fill stroke z0e--z1'e;  % crossbar
 penlabels(0,1,2,3,4,5);
 path p.r;
 p.r=if serifs: pulled_arc.r(3,4)...{x5-x4,5(y5-y4)}z5r
  else: term.r(4',5,right,1,4) fi;
 def the_pen=
  if known ogonek_pen: ogonek_pen elseif serifs: tiny else: fine fi
 enddef;
 numeric tt; % the time...
 tt=if serifs: if hefty: 2.6 else: 2.5 fi else: % sansserif
  if is_bold: .6 else: .8 fi\\ fi;
 join_angle=angle((direction tt of p.r) rotated 180);
 ogonek_breadth=if serifs and is_bold: slab else: xvair fi;
 ogonek_pos=(point tt of p.r)+.5(the_pen-if serifs: tiny else: fine fi)*
  unitvector((direction tt of p.r) rotated 90);
 put_ogonek(the_pen);
endchar;

iff not variant_g: ecchar "The letter g with breve";
beginchar(oct"247",9u#,x_height#+acc_height#,desc_depth#);
italcorr x_height#*slant+.25u#;
adjust_fit(0,0);
numeric light_vair,light_curve,loop_top,loop_side;
light_vair=Vround(.5[thin_join,vair]+vair_corr);
light_curve=max(fine.breadth,hround(curve-3stem_corr));
loop_top=Vround .77[vair,fudged.stem];
loop_side=hround .64[vair,fudged.stem];
pickup fine.nib; pos1(light_vair,90);
pos2(light_curve,180); pos3(light_vair,270);
pos4(light_curve,360); pos11(loop_top,90);
y11r=good.y(y11r+.15bar_height-y11); x1=x3=.5[x2,x4]; y2=y4=.5[y1,y3];
lft x2r=hround(1.75u-.5light_curve); rt x4r=hround(w-2.75u+.5light_curve);
top y1r=x_height+oo; y3r=good.y(y3r+.27[top y11r,bot y1l]-y3);
filldraw stroke pulled_arc.e(1,2) & pulled_arc.e(2,3);  % left half of bowl
filldraw stroke pulled_arc.e(3,4) & pulled_arc.e(4,1);  % right half of bowl
x0=superness[x1r,x4r]; y0=superness[y4r,y1r];  % NE point on the super bowl
x8'=superness[x3r,x2r]; y8'=superness[y2r,y3r];  % SW point on the super bowl
x8''=superness[x3,x2]; y8''=superness[y2,y3];
pos0(thin_join,angle(z0-z8')+90);
pos8(thin_join,angle(z0-z8')-90); z8=.618[z8',z8''];
if serifs: pos5(vair,90);
 pos6(hair,0); pos7(.5[hair,flare],0); x5=.75[x0,x6]; top y5r=x_height+o;
 rt x6r=hround(w-.25u); y6+(.5[hair,flare])/2=.95[bar_height,x_height]+oo;
 filldraw stroke z0e..{right}z5e; bulb(5,6,7);  % ear
else: pos5(vair',100); top y5r=x_height+oo;
 rt x5l=hround(w-.25u); y5l:=good.y y5l;
 filldraw z0l{z5-z0}..z5l--z5r{left}..{curl 1}z0r--cycle; fi  % ear
pos10(loop_top,90); x10=x8+.75u; y10=y11;
pos12(loop_side,0); pos13(light_vair,-90);
pos14(loop_side,-180); pos10'(.5[thin_join,vair],-270);
rt x12r=hround max(w-1.25u+.5loop_side,w-.5u); y12=y14=.5[y11,y13];
x11=x13=max(.5w,x10+eps); bot y13r=-d-oo-1; x14=w-x12; z10'l=z10l;
filldraw stroke z8e{z8-z0}..z10e---z11e;  % link
filldraw stroke {{interim superness:=hein_super; super_arc.e(11,12)}}
 & super_arc.e(12,13) & super_arc.e(13,14) & super_arc.e(14,10');  % loop
% the accent
lowercase_breve(.5w,0,15,16,17);
penlabels(0,1,2,3,4,5,6,7,8,10,11,12,13,14); labels(8',8''); endchar;

iff variant_g: ecchar "Variant letter g with breve";
beginchar(oct"247",10u#+serif_fit#,
    max(x_height#+acc_height#,min(asc_height#,2x_height#)),desc_depth#);
italcorr x_height#*slant-serif_fit#+.5stem#-2u# if serifs:+.5u# fi;
adjust_fit(0,serif_fit# if serifs: -.5u# fi);
pickup tiny.nib; pos1(stem',0); pos2(stem,0);
pos0'(stem',0); pos0(stem,0); z0r=z0'r; x0'=x1; x0=x2;
rt x1r=hround(w-2.5u+.5stem');
numeric edge; edge=lft x2l;
path edge_path; edge_path=(edge,x_height)--(edge,0);
pickup fine.nib; pos3(if hefty:thin_join else: hair fi,0);
pos4(vair,90); pos5(curve,180); pos6(vair,270); penpos7(x3r-x3l,360);
lft x3l=min(lft x3l-(rt x3r-tiny.rt x2r),2/3[lft x2,edge]); y3=bar_height;
x4l=.5(w-serif_fit)-.3u; top y4r=x_height+oo;
lft x5r=hround max(1.35u-.5curve,.6u); y5=.5x_height;
x6l=x4l-.2u; bot y6r=vround 1/3vair;
lft x7l=edge; y7=min(y3,y6+y4-y3+.6vair);
(x,y4r)=whatever[z3l,z4l]; x4r:=max(x,.5[x5r,x4]);
(x',y6r)=whatever[z7l,z6l]; x6r:=max(x',.5[x5r,x6]);
filldraw stroke z3e{up}...{left}z4e&super_arc.e(4,5)
 &super_arc.e(5,6)&z6e{right}...{up}z7e;  % bowl
y1=ypart(edge_path intersectionpoint(z3l{up}...{left}z4l));
y0=ypart(edge_path intersectionpoint(z7l{down}...{left}z6l));
pickup tiny.nib; bot y2=if serifs: -.25d else: 0 fi;
filldraw stroke z1e--z0'e--z0e--z2e;  % stem
pickup crisp.nib;
pos8(hround(hair-stem_corr),0); pos7'(stem',0);
z7'=z1; x8r=x7'r; top y8=x_height+oo;
filldraw stroke z7'e--z8e;  % point
if serifs: pickup tiny.nib;
 pos9(vair,-90); x9=.5[x2,x10]; bot y9r=-d-o-1;
 pos10(hair,-180); lft x10r=hround u; y10=-.75d+.5flare;
 pos11(flare,-180); z11r=z10r;
 bulb(9,10,11); filldraw stroke super_arc.e(2,9);  % tail
else: pickup fine.nib; pos2'(stem,0); z2'=z2;
 z2''r=z2'r; z2''=z2'; z2''l=(x2'l,0);
 pos9(vair,-90); x9=4.5u; bot y9r=-d-o-1;
 pos10(.5[vair,flare],-90); lft x10=hround 1.25u;
 y10r=good.y -5/6d; y10l:=good.y y10l;
 filldraw stroke z2'e..z2''e&super_arc.e(2'',9)
  & term.e(9,10,left,.9,4); fi  % tail
% the accent
lowercase_breve(.5w,0,12,13,14);
penlabels(0,1,2,3,4,5,6,7,8,9,10,11,12,13,14); endchar;

ecchar "The letter l with acute"; % jk
beginchar_twice(oct"250",5u#,asc_height#,0); 
l_width#:=5u#+2serif_fit#;
italcorr asc_height#*slant-serif_fit#+.5stem#-2u#;
adjust_fit(serif_fit#+stem_shift#,serif_fit#-stem_shift#);
pickup tiny.nib; pos1(stem',0); pos2(stem',0);
if odd(w-stem'): change_width; fi
lft x1l=hround(.5w-.5stem'); x1=x2; top y1=asc_height; bot y2=0;
filldraw stroke z1e--z2e;  % stem
if serifs or monospace:
 sloped_serif.l(1,2,a,1/3,jut,serif_drop);  % upper serif
fi
if serifs: dish_serif(2,1,b,1/3,jut,c,1/3,jut); fi  % lower serif
put_accent;
repeat_once;
penlabels(1,2); endchar;

ecchar "The letter l with hachek"; % from csotherl.mf (csfonts)
beginchar(oct"251",5u#,lc_acute_height#,0); l_width#:=5u#+2serif_fit#;
italcorr lc_acute_height#*slant-serif_fit#+.5stem#-2u#;
adjust_fit(serif_fit#+stem_shift#,
  if monospace: 0 else: dot_size#+0.25u# fi);
pickup tiny.nib; pos1(stem',0); pos2(stem',0);
if odd(w-stem'): change_width; fi
lft x1l=hround(.5w-.5stem'); x1=x2; top y1=asc_height; bot y2=0;
filldraw stroke z1e--z2e;  % stem
if serifs or monospace:
 sloped_serif.l(1,2,a,1/3,jut,serif_drop);  % upper serif
fi
if serifs:
 dish_serif(2,1,b,1/3,jut,c,1/3,jut); fi  % lower serif
penlabels(1,2);
dtl_hachek(x1r+u+.5pdot_diam)(y1);
endchar;

ecchar "The letter crossed l"; % from pl_ml (plfonts)
beginchar_twice(oct"252",5u#,asc_height#,0);
 italcorr asc_height#*slant-serif_fit#+.5stem#-2u#;
 charwd_:=charwd;
 adjust_fit(serif_fit#+stem_shift#,serif_fit#-stem_shift#);
 pickup tiny.nib; pos1(stem',0); pos2(stem',0);
 if odd(w-stem'): change_width; fi
 lft x1l=hround(.5w-.5stem'); x1=x2; top y1=h; bot y2=0;
 filldraw stroke z1e--z2e; % stem
 if serifs or monospace:
  sloped_serif.l(1,2,a,1/3,jut,serif_drop); % upper serif
 fi
 if serifs: dish_serif(2,1,b,1/3,jut,c,1/3,jut); fi % lower serif
 penlabels(1,2);
 numeric unbalance;
 unbalance=if monospace: 1 else: 1.1 fi;
 if not monospace:
  numeric cross_fit#; 
  cross_fit#=serif_fit#+if not serifs: if is_bold: 1 else: .75 fi\\ else:
   .5 fi\\ u#;
  charwd:=charwd_;
  adjust_fit(cross_fit#+stem_shift#,unbalance*cross_fit#-stem_shift#);
 fi
 numeric l_jut;
% some funny fonts have an extremally small |jut|:
 l_jut=.5stem+if serifs: max(limit_dist,if hefty: .8 fi\\ jut) else: 1.7u fi;
 put_cross(l_jut,unbalance*l_jut);
repeat_once;
endchar;

ecchar "The letter n with acute"; % from pl_ml (plfonts)
beginchar_twice(oct"253",10u#,x_height#,0);
 italcorr .5[bar_height#,x_height#]*slant-serif_fit#+.5stem#-2u#;
 adjust_fit(serif_fit#+stem_shift#,serif_fit#-stem_shift#);
 pickup tiny.nib; pos1(stem,0); pos2(stem,0);
 numeric shaved_stem; shaved_stem=hround(stem-2stem_corr);
 pos1'(shaved_stem,0); pos2'(shaved_stem,0); pos3(stem,0);
 lft x1l=hround(2.5u-.5stem); x1l=x1'l=x2l=x2'l; x3=w-x1;
 top y1=h+min(oo,serif_drop); bot y2=0; y1=y1'; y2=y2';
 filldraw stroke z1'e--z2'e;  % left stem
 h_stroke(2,a,3,4);  % arch and right stem
 if serifs: sloped_serif.l(1',2',b,1/3,jut,serif_drop); % upper left serif
  numeric inner_jut; pickup tiny.nib;
  if rt x2r+jut+.5u+1<=lft x4l-jut: inner_jut=jut;
  else: rt x2r+jut+.5u+1=lft x4l-inner_jut; fi
  dish_serif(2,1,c,1/3,jut,d,1/3,jut); % lower left serif
  dish_serif(4,3,e,1/3,inner_jut,f,1/3,jut); fi % lower right serif
 hpos_corr=-.5u; put_accent;
repeat_once;
penlabels(1,2,3,4); endchar;

ecchar "The letter n with hachek"; % from cshachel.mf (csfonts)
beginchar(oct"254",10u#,lc_hachek_height#,0);
italcorr .5[bar_height#,x_height#]*slant-serif_fit#+.5stem#-2u#;
adjust_fit(serif_fit#+stem_shift#,serif_fit#-stem_shift#);
pickup tiny.nib; pos1(stem,0); pos2(stem,0);
numeric shaved_stem; shaved_stem=hround(stem-2stem_corr);
pos1'(shaved_stem,0); pos2'(shaved_stem,0); pos3(stem,0);
lft x1l=hround(2.5u-.5stem); x1l=x1'l=x2l=x2'l; x3=w-x1;
top y1=x_height+min(oo,serif_drop); bot y2=0; y1=y1'; y2=y2';
filldraw stroke z1'e--z2'e;  % left stem
h_stroke(2,a,3,4);  % arch and right stem
if serifs: sloped_serif.l(1',2',b,1/3,jut,serif_drop); % upper left serif
 numeric inner_jut; pickup tiny.nib;
 if rt x2r+jut+.5u+1<=lft x4l-jut: inner_jut=jut;
 else: rt x2r+jut+.5u+1=lft x4l-inner_jut; fi
 dish_serif(2,1,c,1/3,jut,d,1/3,jut); % lower left serif
 dish_serif(4,3,e,1/3,inner_jut,f,1/3,jut); fi % lower right serif
penlabels(1,2,3,4); 
lc_hachek_accent(1/2[x1r,x.a1]);
endchar;


ecchar "The letter eng";
beginchar(oct"255",10u#,x_height#,desc_depth#);
italcorr .5[bar_height#,x_height#]*slant-serif_fit#+.5stem#-2u#;
adjust_fit(serif_fit#+stem_shift#,serif_fit#-stem_shift#);
pickup tiny.nib; pos1(stem,0); pos2(stem,0);
numeric shaved_stem; shaved_stem=hround(stem-2stem_corr);
pos1'(shaved_stem,0); pos2'(shaved_stem,0); pos3(stem,0);
lft x1l=hround(2.5u-.5stem); x1l=x1'l=x2l=x2'l; x3=w-x1;
top y1=h+min(oo,serif_drop); bot y2=0; y1=y1'; y2=y2';
filldraw stroke z1'e--z2'e;  % left stem
h_stroke(2,a,3,4);  % arch and right stem
if serifs: sloped_serif.l(1',2',b,1/3,jut,serif_drop); % upper left serif
 numeric inner_jut; pickup tiny.nib;
 if rt x2r+jut+.5u+1<=lft x4l-jut: inner_jut=jut;
 else: rt x2r+jut+.5u+1=lft x4l-inner_jut; fi
 dish_serif(2,1,c,1/3,jut,d,1/3,jut); % lower left serif
% j stroke
 pickup tiny.nib; pos7(vair,-90); pos8(hair,-180); pos9(flare,-180);
 x7=.5[x4,x8r]; bot y7r=-d-oo; y8-.5flare=-.88d;
 if monospace:
 lft x8r=hround(x3r-3u+0.5stem);
 else:
 z8r=z9r; rt x9l=hround(x3r-3u+0.5stem); % floor .75u
 fi;
 (x,y7r)=whatever[z7l,z4l]; x7r:=max(x,.5[x8r,x7]);
 filldraw stroke z4e{down}...z7e{left};  bulb(7,8,9); % arc and bulb
else: pickup fine.nib; pos4'(stem',0); z4'=z4;
 pos8(.2[vair,stem'],-90); pos9(vair,-90);
 lft x9r=hround -.75u+x3r-3u-0.5stem;
 bot y9r=vround 5/6(-d-oo);
 (x,y9l)=whatever[z9r,z3];
 x9l:=x;
 z7r=z4'r;
 (x4'l,y7l)=whatever[z9l,z7r]; x7l=x4'l; y7=y7r;
 x8r=.5[x9r,x7r]; x8l:=.5[x9l,x7l]; bot y8r=-d-oo;
 filldraw stroke z4'e..{down}z7e
 & super_arc.e(7,8)
 & z8e{left}..z9e;
fi  % arc and terminal
penlabels(1,2,3,4,5,6,7,8,9); endchar;

ecchar "The letter o with double acute";
beginchar_twice(oct"256",9u#,x_height#+acc_height#,0);
italcorr .7x_height#*slant;
adjust_fit(if monospace: .5u#,.5u# else: 0,0 fi);
penpos1(vair,90); penpos3(vair',-90);
penpos2(curve,180); penpos4(curve,0);
x2r=hround max(.5u,1.25u-.5curve);
x4r=w-x2r; x1=x3=.5w; y1r=x_height+vround 1.5oo; y3r=-oo;
y2=y4=.5x_height-vair_corr; y2l:=y4l:=.52x_height;
penstroke pulled_arc.e(1,2) & pulled_arc.e(2,3)
 & pulled_arc.e(3,4) & pulled_arc.e(4,1) & cycle;  % bowl
if monospace: hpos_corr=-2u; put_accent; hpos_corr=2u; put_accent;
else: hpos_corr=-1.5u; put_accent; hpos_corr=1.5u; put_accent; fi
repeat_once;
penlabels(1,2,3,4,5,6,7,8); endchar;


ecchar "The letter r with acute";
numeric r_flare#; r_flare#=.75[if serifs: stem# else: vair# fi,flare#];
beginchar_twice(oct"257",if serifs:max(7u#,5.5u#+r_flare#) else:6.5u# fi,
   x_height#+acc_height#,0);
italcorr x_height#*slant if not serifs: +.25u# fi;
adjust_fit(serif_fit#,0);
define_whole_blacker_pixels(r_flare); % must come after |beginchar_twice|
pickup fine.nib; top y4r=x_height+oo;
if serifs: pos4(vair,90); pos5(hair,0);
 x4=w-max(1.75u,.25u+r_flare);
 rt x5r=hround(w-.5u+.5); y5+.5r_flare=.9[bar_height,x_height]+oo;
 pos6(r_flare,0); bulb(4,5,6);  % bulb
else: pos4(r_flare,90); rt x4=hround(w-.25u); fi
pos3(thin_join,180); rt x3l=hround(2.5u-.5stem')+stem'; top y3=bar_height;
filldraw stroke z3e{up}...{right}z4e;  % arc
pickup tiny.nib; pos0(stem',0); pos2(stem',0);
pos1(hround(stem-3stem_corr),0); top y1=x_height+min(oo,serif_drop);
pos0'(hround(stem-3stem_corr),0); y0=y0'=y3; x1l=x0l=x0'l=x2l;
lft x1l=hround(2.5u-.5stem'); bot y2=0;
filldraw stroke z1e--z0'e--z0e--z2e;  % stem
if serifs: sloped_serif.l(1,0',a,1/3,jut,serif_drop);  % upper serif
 dish_serif(2,0,b,1/3,jut,c,1/3,1.25jut); fi  % lower serif
% the accent
hpos_corr=-.25u; put_accent;
repeat_once;
penlabels(1,2,3,4,5,6,7,8); endchar;


ecchar "The letter r with hachek"; % from cshachel.mf (csfonts)
numeric r_flare#; r_flare#=.75[if serifs: stem# else: vair# fi,flare#];
define_whole_blacker_pixels(r_flare);
beginchar(oct"260",if serifs:max(7u#,5.5u#+r_flare#) else:6.5u# fi,
   lc_hachek_height#,0);
italcorr x_height#*slant if not serifs: +.25u# fi;
adjust_fit(serif_fit#,0);
pickup fine.nib; top y4r=x_height+oo;
if serifs: pos4(vair,90); pos5(hair,0);
 x4=w-max(1.75u,.25u+r_flare);
 rt x5r=hround(w-.5u+.5); y5+.5r_flare=.9[bar_height,x_height]+oo;
 pos6(r_flare,0); bulb(4,5,6);  % bulb
else: pos4(r_flare,90); rt x4=hround(w-.25u); fi
pos3(thin_join,180); rt x3l=hround(2.5u-.5stem')+stem'; top y3=bar_height;
filldraw stroke z3e{up}...{right}z4e;  % arc
pickup tiny.nib; pos0(stem',0); pos2(stem',0);
pos1(hround(stem-3stem_corr),0); top y1=x_height+min(oo,serif_drop);
pos0'(hround(stem-3stem_corr),0); y0=y0'=y3; x1l=x0l=x0'l=x2l;
lft x1l=hround(2.5u-.5stem'); bot y2=0;
filldraw stroke z1e--z0'e--z0e--z2e;  % stem
if serifs: sloped_serif.l(1,0',a,1/3,jut,serif_drop);  % upper serif
 dish_serif(2,0,b,1/3,jut,c,1/3,1.25jut); fi  % lower serif
penlabels(1,2,3,4,5,6);
lc_hachek_accent(0.5[x1,x4]);
endchar;

ecchar "The letter s with acute"; % from pl_ml (plfonts)
beginchar_twice(oct"261",7.1u#,x_height#,0);
 italcorr x_height#*slant-if serifs:.55u# else:.3u# fi;
 adjust_fit(if monospace: .5u#,.4u# else: 0,if serifs:0 else:-.2u# fi fi);
 numeric theta; theta=90-angle(40u,h); slope:=-h/40u;  % angle at middle
 numeric s_slab; s_slab=if serifs:vair else:Vround .1[vair,stem] fi;
 numeric ess'; ess'=max(fine.breadth,ess);
 pickup fine.nib; pos2(max(fine.breadth,s_slab-vround vair_corr),-100);
 pos0(ess',theta); pos7(s_slab,-90); x2l=x0=x7=.5w;
 top y2l=h+vround 1.5oo; bot y7r=-oo;
 y0-.5ess'=y7l+if serifs:.54 else: .52 fi\\(y2r-y7l-ess');
 lft x3l=hround .6u; rt x6r=hround(w-.6u);
 x3r-x3l=x6r-x6l=hround .5[s_slab,ess']-fine;
 ellipse_set(2l,3l,4l,0l); ellipse_set(2r,3r,4r,0r); y3=y3r;
 ellipse_set(7l,6l,5l,0l); ellipse_set(7r,6r,5r,0r); y6=y6r;
 interim superness:=more_super;
 filldraw stroke super_arc.e(2,3) & z3e{down}
  ..z4e---z5e..z6e{down} & super_arc.e(6,7);  % main stroke
 if serifs: pos1(hair,180); pos8(hair,180);
  rt x1l=hround(w-1.05u); lft x8r=hround .6u;
  bot y1=min(bot y2r,vround 1/5[top y5r,h]);
  top y8=max(top y7l,vround 5/6 bot y4l);
  filldraw stroke z1e{up}....{left}z2e;  % upper arc
  filldraw stroke z7e{left}....{x8-x7,5(y8-y7)}z8e;  % lower arc
  path upper_arc, lower_arc;
  upper_arc=z1{up}....{left}z2; lower_arc=z7{left}....{x8-x7,5(y8-y7)}z8;
  pos10(.3[fine.breadth,cap_hair],0); pos9(.3[fine.breadth,cap_hair],0);
  x10r=x1l; top y10=top y2l; x9l=x8r; bot y9=bot y7r;
  x1l-x1'=x8'-x8r=1.6cap_curve-fine; y1'=y1; y8'=y8;
  numeric t; t=xpart(upper_arc intersectiontimes(z10l--z1'));
  filldraw z1l--z10r--z10l--subpath(t,0) of upper_arc--cycle;  % upper barb
  t:=xpart(lower_arc intersectiontimes(z9r--z8'));
  filldraw z8r--z9l--z9r--subpath(t,1) of lower_arc--cycle;  % lower barb
 else: pos1(4/7[s_slab,flare],-100); pos8(flare,-100);
  x1l=good.x(x1l+w-u-rt x1); lft x8r=hround .5u;
  top y1l=vround(.93h+1.5oo); bot y8r=vround .1h-oo;
  filldraw stroke term.e(2,1,right,.9,4);  % upper arc and terminal
  filldraw stroke term.e(7,8,left,1,4); fi  % lower arc and terminal
 penlabels(0,1,1',2,3,4,5,6,7,8,8',9,10);
 hpos_corr=-.5u; put_accent;
repeat_once;
endchar;

ecchar "The letter s with hachek"; % from cshachel.mf (csfonts)
beginchar(oct"262",7.1u#,lc_hachek_height#,0);
italcorr x_height#*slant-if serifs:.55u# else:.3u# fi;
adjust_fit(if monospace: .5u#,.4u# else: 0,if serifs:0 else:-.2u# fi fi);
numeric theta; theta=90-angle(40u,h); slope:=-x_height/40u;  % angle at middle
numeric s_slab; s_slab=if serifs:vair else:Vround .1[vair,stem] fi;
numeric ess'; ess'=max(fine.breadth,ess);
pickup fine.nib; pos2(max(fine.breadth,s_slab-vround vair_corr),-100);
pos0(ess',theta); pos7(s_slab,-90); x2l=x0=x7=.5w;
top y2l=x_height+vround 1.5oo; bot y7r=-oo;
y0-.5ess'=y7l+if serifs:.54 else: .52 fi\\(y2r-y7l-ess');
lft x3l=hround .6u; rt x6r=hround(w-.6u);
x3r-x3l=x6r-x6l=hround .5[s_slab,ess']-fine;
ellipse_set(2l,3l,4l,0l); ellipse_set(2r,3r,4r,0r); y3=y3r;
ellipse_set(7l,6l,5l,0l); ellipse_set(7r,6r,5r,0r); y6=y6r;
interim superness:=more_super;
filldraw stroke super_arc.e(2,3) & z3e{down}
 ..z4e---z5e..z6e{down} & super_arc.e(6,7);  % main stroke
if serifs: pos1(hair,180); pos8(hair,180);
 rt x1l=hround(w-1.05u); lft x8r=hround .6u;
 bot y1=min(bot y2r,vround 1/5[top y5r,x_height]);
 top y8=max(top y7l,vround 5/6 bot y4l);
 filldraw stroke z1e{up}....{left}z2e;  % upper arc
 filldraw stroke z7e{left}....{x8-x7,5(y8-y7)}z8e;  % lower arc
 path upper_arc, lower_arc;
 upper_arc=z1{up}....{left}z2; lower_arc=z7{left}....{x8-x7,5(y8-y7)}z8;
 pos10(.3[fine.breadth,cap_hair],0); pos9(.3[fine.breadth,cap_hair],0);
 x10r=x1l; top y10=top y2l; x9l=x8r; bot y9=bot y7r;
 x1l-x1'=x8'-x8r=1.6cap_curve-fine; y1'=y1; y8'=y8;
 numeric t; t=xpart(upper_arc intersectiontimes(z10l--z1'));
 filldraw z1l--z10r--z10l--subpath(t,0) of upper_arc--cycle;  % upper barb
 t:=xpart(lower_arc intersectiontimes(z9r--z8'));
 filldraw z8r--z9l--z9r--subpath(t,1) of lower_arc--cycle;  % lower barb
else: pos1(4/7[s_slab,flare],-100); pos8(flare,-100);
 x1l=good.x(x1l+w-u-rt x1); lft x8r=hround .5u;
 top y1l=vround(.93x_height+1.5oo); bot y8r=vround .1x_height-oo;
 filldraw stroke term.e(2,1,right,.9,4);  % upper arc and terminal
 filldraw stroke term.e(7,8,left,1,4); fi  % lower arc and terminal
penlabels(0,1,1',2,3,4,5,6,7,8,8',9,10);
lc_hachek_accent(0.5[x3l,x1l]);
endchar;

ecchar "The letter cedilla s";
beginchar(oct"263",7.1u#,x_height#,0.875desc_depth#);
italcorr x_height#*slant-if serifs:.55u# else:.3u# fi;
adjust_fit(if monospace: .5u#,.4u# else: 0,if serifs:0 else:-.2u# fi fi);
numeric theta; theta=90-angle(40u,h); slope:=-x_height/40u;  % angle at middle
numeric s_slab; s_slab=if serifs:vair else:Vround .1[vair,stem] fi;
numeric ess'; ess'=max(fine.breadth,ess);
pickup fine.nib; pos2(max(fine.breadth,s_slab-vround vair_corr),-100);
pos0(ess',theta); pos7(s_slab,-90); x2l=x0=x7=.5w;
top y2l=x_height+vround 1.5oo; bot y7r=-oo;
y0-.5ess'=y7l+if serifs:.54 else: .52 fi\\(y2r-y7l-ess');
lft x3l=hround .6u; rt x6r=hround(w-.6u);
x3r-x3l=x6r-x6l=hround .5[s_slab,ess']-fine;
ellipse_set(2l,3l,4l,0l); ellipse_set(2r,3r,4r,0r); y3=y3r;
ellipse_set(7l,6l,5l,0l); ellipse_set(7r,6r,5r,0r); y6=y6r;
interim superness:=more_super;
filldraw stroke super_arc.e(2,3) & z3e{down}
 ..z4e---z5e..z6e{down} & super_arc.e(6,7);  % main stroke
if serifs: pos1(hair,180); pos8(hair,180);
 rt x1l=hround(w-1.05u); lft x8r=hround .6u;
 bot y1=min(bot y2r,vround 1/5[top y5r,x_height]);
 top y8=max(top y7l,vround 5/6 bot y4l);
 filldraw stroke z1e{up}....{left}z2e;  % upper arc
 filldraw stroke z7e{left}....{x8-x7,5(y8-y7)}z8e;  % lower arc
 path upper_arc, lower_arc;
 upper_arc=z1{up}....{left}z2; lower_arc=z7{left}....{x8-x7,5(y8-y7)}z8;
 pos10(.3[fine.breadth,cap_hair],0); pos9(.3[fine.breadth,cap_hair],0);
 x10r=x1l; top y10=top y2l; x9l=x8r; bot y9=bot y7r;
 x1l-x1'=x8'-x8r=1.6cap_curve-fine; y1'=y1; y8'=y8;
 numeric t; t=xpart(upper_arc intersectiontimes(z10l--z1'));
 filldraw z1l--z10r--z10l--subpath(t,0) of upper_arc--cycle;  % upper barb
 t:=xpart(lower_arc intersectiontimes(z9r--z8'));
 filldraw z8r--z9l--z9r--subpath(t,1) of lower_arc--cycle;  % lower barb
else: pos1(4/7[s_slab,flare],-100); pos8(flare,-100);
 x1l=good.x(x1l+w-u-rt x1); lft x8r=hround .5u;
 top y1l=vround(.93x_height+1.5oo); bot y8r=vround .1x_height-oo;
 filldraw stroke term.e(2,1,right,.9,4);  % upper arc and terminal
 filldraw stroke term.e(7,8,left,1,4); fi  % lower arc and terminal
% the accent
lowercase_cedilla(x7,0,11,12,13,14,15);
penlabels(0,1,1',2,3,4,5,6,7,8,8',9,10); endchar;

ecchar "The letter t with hachek"; % from csotherl.mf (csfonts)
%beginchar(oct"264",6u#+max(u#,.5stem#),
% min(asc_height#,if hefty:9/7 else:10/7 fi\\ x_height#),0);
beginchar(oct"264",6u#+max(u#,.5stem#),lc_acute_height#,0);
numeric hh';
hh' = min(asc_height,if hefty:9/7 else:10/7 fi\\ x_height);
italcorr lc_acute_height#*slant if serifs: -.9u# else: -.4u# fi;
adjust_fit(0,if serifs: 0 else: -.5u# fi);
numeric shaved_stem; shaved_stem=hround(stem if hefty:-\\2stem_corr fi);
pickup fine.nib; pos2(shaved_stem,180);pos3(shaved_stem,180);
lft x2r=lft x3r=hround(2.5u-.5shaved_stem);
y2=y8; y3=max(.5bar_height,2vair);
pickup crisp.nib; pos8(bar,90);
rt x8=hround(w-1.3u); top y8r=x_height; lft x7=hround 1/3u; y7l=y8l;
if hefty: pos7(bar,90);
 filldraw stroke z7e--z8e;  % crossbar
 pickup tiny.nib; pos1(hround(shaved_stem-stem_corr),0);
 rt x1r=fine.rt x2l; top y1=hh';
 penpos2'(x1r-x1l,0); x2'=x1; y2'=y2;
 filldraw stroke z1e--z2'e;  % upper terminal
else: pos7(vair,90); pos1(hair,0);
 rt x1r=fine.rt x2l; top y1=hh';
 filldraw z1l{down}...{left}z7r--z7l--z8l
  --z8r--(x1r,y8r)--z1r--cycle; fi  % upper terminal and crossbar
pickup fine.nib; interim superness:=more_super;
pos4(vair',-90); bot y4r=-oo; rt x5r=hround(w-u);
if serifs: pos5(hair,0); y5=y3; x4l=.5[x3l,x5l];
 (x,y4r)=whatever[z4l,z5l]; x4r:=max(x,.5[x3r,x4]);
 filldraw stroke z2e..super_arc.e(3,4)...{up}z5e;  % stem and hook
 pickup crisp.nib; pos6(hair,0); pos5'(hair,0);
 x6=x5=x5'; top y6=max(vround .75bar_height,top y5); y5=y5';
 filldraw stroke z5'e--z6e;  % terminal
else: pos5(vair,-75); top y5l=vround .2[top y4l,bar_height];
 x5l:=good.x x5l; x4l=1/3[x3l,x5l]; x4r:=1/3[x3r,x5r]; y3l:=y3l+.2vair;
 filldraw stroke z2e..super_arc.e(3,4);  % stem and hook
 path p; p=stroke z4e{right}..tension .9 and atleast 1..z5e;  % terminal
 if (xpart(z5l-precontrol 1 of p)<0) or (xpart(z5r-postcontrol 2 of p)<0):
  filldraw stroke z4e{right}...{up}z5e;
 else: filldraw p; fi fi;
penlabels(1,2,3,4,5,6,7,8);
dtl_hachek(x8)(y1+.5pdot_diam);
endchar;

ecchar "The letter cedilla t";
beginchar(oct"265",6u#+max(u#,.5stem#),
 min(asc_height#,if hefty:9/7 else:10/7 fi\\ x_height#),0.875desc_depth#);
italcorr x_height#*slant if serifs: -.9u# else: -.4u# fi;
adjust_fit(0,if serifs: 0 else: -.5u# fi);
numeric shaved_stem; shaved_stem=hround(stem if hefty:-\\2stem_corr fi);
pickup fine.nib; pos2(shaved_stem,180);pos3(shaved_stem,180);
lft x2r=lft x3r=hround(2.5u-.5shaved_stem);
y2=y8; y3=max(.5bar_height,2vair);
pickup crisp.nib; pos8(bar,90);
rt x8=hround(w-1.3u); top y8r=x_height; lft x7=hround 1/3u; y7l=y8l;
if hefty: pos7(bar,90);
 filldraw stroke z7e--z8e;  % crossbar
 pickup tiny.nib; pos1(hround(shaved_stem-stem_corr),0);
 rt x1r=fine.rt x2l; top y1=h;
 penpos2'(x1r-x1l,0); x2'=x1; y2'=y2;
 filldraw stroke z1e--z2'e;  % upper terminal
else: pos7(vair,90); pos1(hair,0);
 rt x1r=fine.rt x2l; top y1=h;
 filldraw z1l{down}...{left}z7r--z7l--z8l
  --z8r--(x1r,y8r)--z1r--cycle; fi  % upper terminal and crossbar
pickup fine.nib; interim superness:=more_super;
pos4(vair',-90); bot y4r=-oo; rt x5r=hround(w-u);
if serifs: pos5(hair,0); y5=y3; x4l=.5[x3l,x5l];
 (x,y4r)=whatever[z4l,z5l]; x4r:=max(x,.5[x3r,x4]);
 filldraw stroke z2e..super_arc.e(3,4)...{up}z5e;  % stem and hook
 pickup crisp.nib; pos6(hair,0); pos5'(hair,0);
 x6=x5=x5'; top y6=max(vround .75bar_height,top y5); y5=y5';
 filldraw stroke z5'e--z6e;  % terminal
else: pos5(vair,-75); top y5l=vround .2[top y4l,bar_height];
 x5l:=good.x x5l; x4l=1/3[x3l,x5l]; x4r:=1/3[x3r,x5r]; y3l:=y3l+.2vair;
 filldraw stroke z2e..super_arc.e(3,4);  % stem and hook
 path p; p=stroke z4e{right}..tension .9 and atleast 1..z5e;  % terminal
 if (xpart(z5l-precontrol 1 of p)<0) or (xpart(z5r-postcontrol 2 of p)<0):
  filldraw stroke z4e{right}...{up}z5e;
 else: filldraw p; fi fi;
% the accent
lowercase_cedilla(x4,0,9,10,11,12,13);
penlabels(1,2,3,4,5,6,7,8,9,10,1,12,13); endchar;

ecchar "The letter u with double acute";
beginchar_twice(oct"266",10u#,x_height#+acc_height#,0);
italcorr x_height#*slant-serif_fit#+.5stem#-2u#;
adjust_fit(serif_fit#+stem_shift#,serif_fit#-stem_shift#);
numeric light_vair; light_vair=vair if hefty: -vround 2vair_corr fi;
if light_vair<fine.breadth: light_vair:=fine.breadth; fi
pickup tiny.nib; pos1(stem,0); pos2(stem,0);
pos3(stem,0); pos4(stem',0);
lft x1l=hround(2.5u-.5stem); x1=x2; x3=w-x1; x3r=x4r;
if serifs: top y1=x_height+min(oo,serif_drop); bot y4=-min(oo,serif_drop);
else: top y1=x_height; bot y4=0; fi
pos0(stem,0); pos0'(stem',0); x0=x3; x0'=x4; y0=y0';
penpos2'(stem-fine,-180); z2'=z2; y3=y1; y2=.5bar_height;
penpos5(max(eps,light_vair-fine),-90); penpos6(thin_join-fine,0);
y6=y0=2/3bar_height;
filldraw stroke z1e--z2e;  % left stem
filldraw stroke z3e..z0e--z0'e..z4e;  % right stem
pickup fine.nib; bot y5r=-oo; x5l=.5w-.25u; lft x6l=tiny.lft x0l;
(x,y5r)=whatever[z5l,z6l]; x5r:=max(x,.5[x5,x2'r]);
filldraw stroke {{interim superness:=hein_super;
  pulled_arc.e(2',5)}} & z5e{right}...{up}z6e;  % arc
if serifs: sloped_serif.l(1,2,a,1/3,jut,serif_drop); % upper left serif
 sloped_serif.l(3,0,b,1/3,jut,serif_drop); % upper right serif
 sloped_serif.r(4,0',c,1/3,jut,min(oo,serif_drop)); fi % lower right serif
if monospace: hpos_corr=-2u; put_accent; hpos_corr=2u; put_accent;
else: hpos_corr=-1.5u; put_accent; hpos_corr=1.5u; put_accent; fi
repeat_once;
penlabels(1,2,3,4,5,6,7,8,9,10); endchar;


ecchar "The letter circle u";
beginchar(oct"267",10u#,x_height#+acc_height#,0);
italcorr x_height#*slant-serif_fit#+.5stem#-2u#;
adjust_fit(serif_fit#+stem_shift#,serif_fit#-stem_shift#);
numeric light_vair; light_vair=vair if hefty: -vround 2vair_corr fi;
if light_vair<fine.breadth: light_vair:=fine.breadth; fi
pickup tiny.nib; pos1(stem,0); pos2(stem,0);
pos3(stem,0); pos4(stem',0);
lft x1l=hround(2.5u-.5stem); x1=x2; x3=w-x1; x3r=x4r;
if serifs: top y1=x_height+min(oo,serif_drop); bot y4=-min(oo,serif_drop);
else: top y1=x_height; bot y4=0; fi
pos0(stem,0); pos0'(stem',0); x0=x3; x0'=x4; y0=y0';
penpos2'(stem-fine,-180); z2'=z2; y3=y1; y2=.5bar_height;
penpos5(max(eps,light_vair-fine),-90); penpos6(thin_join-fine,0);
y6=y0=2/3bar_height;
filldraw stroke z1e--z2e;  % left stem
filldraw stroke z3e..z0e--z0'e..z4e;  % right stem
pickup fine.nib; bot y5r=-oo; x5l=.5w-.25u; lft x6l=tiny.lft x0l;
(x,y5r)=whatever[z5l,z6l]; x5r:=max(x,.5[x5,x2'r]);
filldraw stroke {{interim superness:=hein_super;
  pulled_arc.e(2',5)}} & z5e{right}...{up}z6e;  % arc
if serifs: sloped_serif.l(1,2,a,1/3,jut,serif_drop); % upper left serif
 sloped_serif.l(3,0,b,1/3,jut,serif_drop); % upper right serif
 sloped_serif.r(4,0',c,1/3,jut,min(oo,serif_drop)); fi % lower right serif
% the accent
lowercase_circle(.5w,(1/3[x_height,h]+apex_o),7,8,9,10);
penlabels(1,2,3,4,5,6,7,8,9,10); endchar;


ecchar "The letter y with umlaut";
beginchar(oct"270",if serifs:9.5u# else:9u# fi,lc_trema_height#,desc_depth#);
italcorr x_height#*slant+.25u#;
adjust_fit(serif_fit# if monospace:+\\.5u#,.5u#+ else:,fi\\ serif_fit#);
numeric left_stem,right_stem,bot_stem,bot_vair,outer_jut;
left_stem=fudged.stem-stem_corr;
right_stem=fudged.hair if hefty:-2stem_corr fi;
bot_stem=fudged.hair if hefty:-8stem_corr fi;
bot_vair=Vround(if serifs: vair else:.5[vair,bot_stem] fi);
outer_jut=.75jut;
x1l=w-x4r=l+letter_fit+outer_jut+.25u; y1=y4r=x_height; y2=y3=0; x2l=x3l;
numeric alpha,alpha[]; x9=3u; y9=bot_vair-d-oo;
alpha1=diag_ratio(2,bot_stem,y1-y3,x4r-x1l-apex_corr);
alpha2=diag_ratio(1,bot_stem,y1-y9,x4r-x9);
if alpha1<alpha2: x2l-x1l=x4r-x3r+apex_corr; alpha=alpha1;
else: alpha=alpha2; z3l=whatever[z9,z4r-(alpha*bot_stem,0)]; fi
penpos3(alpha*bot_stem,0); penpos4(alpha*right_stem,0);
alpha3=(y1++(x2l-x1l))/y1;
penpos1(alpha3*left_stem,0); penpos2(alpha3*left_stem,0);
z0=whatever[z1r,z2r]=z4l+whatever*(z3r-z4r);
if y0>notch_cut: y0:=notch_cut;
  fill z0+.5right{up}...{z4r-z3r}diag_end(0,4l,1,1,4r,3r)
    --z3r--z2l--diag_end(2l,1l,1,1,1r,2r){z2-z1}
    ...{down}z0+.5left--cycle; % left and right diagonals
else: fill z0--diag_end(0,4l,1,1,4r,3r)--z3r--z2l
    --diag_end(2l,1l,1,1,1r,0)--cycle; fi % left and right diagonals
penpos5(alpha*bot_stem,0); z5r=whatever[z3r,z4r]; y5-.5vair=-.5d;
if serifs: numeric light_bulb; light_bulb=hround 7/8[hair,flare]; clearpen;
 penpos6(vair,-90); penpos7(hair,-180); penpos8(light_bulb,-180);
 x6=2u; y6r=-d-oo; y8-.5light_bulb=-.85d; x8r=hround .35u;
 fill stroke z3e---z5e...{left}z6e; bulb(6,7,8);  % arc and bulb
 numeric inner_jut; pickup tiny.nib;
 prime_points_inside(1,2); prime_points_inside(4,3);
 if rt x1'r+jut+.5u+1<=lft x4'l-jut: inner_jut=jut;
 else: rt x1'r+inner_jut+.5u+1=lft x4'l-inner_jut; fi
 dish_serif(1',2,a,1/3,outer_jut,b,1/2,inner_jut);  % left serif
 dish_serif(4',3,c,.6,inner_jut,d,1/2,outer_jut)(dark);  % right serif
else: penpos6(bot_vair,-90); x6=2.5u; y6r=-d-oo;
 fill stroke z3e---z5e...{left}z6e;  % arc
 pickup fine.nib; pos6'(bot_vair,-90); z6'=z6;
 pos7(2/3[bot_vair,flare],-85);
 lft x7l=hround u; bot y7r=vround-.96d-oo; y7l:=good.y y7l;
 filldraw stroke term.e(6',7,left,1,4); fi % arc and terminal
% the accent
lowercase_umlaut(0,0,10,11,12,13);
penlabels(0,1,2,3,4,5,6,7,8,9,10,11,12,13); endchar;

ecchar "The letter z with acute"; % from pl_ml (plfonts)
beginchar_twice(oct"271",8u#,x_height#,0);
 italcorr x_height#*slant-.5serif_fit#-.3u#;
 adjust_fit(0,.5serif_fit#);
 numeric arm_thickness[\\],z_stem;
 if hefty: arm_thickness1=Vround(vair-vair_corr); arm_thickness2=vair;
  z_stem=.6[vair,stem];
 else: arm_thickness1=vair; arm_thickness2=vair'; z_stem=.9[vair,stem]; fi
 pickup tiny.nib; rt x1r=rt x2r=hround(w-.8u); lft x3l=lft x4l=hround .5u;
 top y1=h; y2=min(y1,h-2/3arm_thickness1);
 bot y4=0; y3=max(y4,2/3arm_thickness2);
 numeric alpha; 
 alpha=diag_ratio(1,z_stem-tiny,y2-y3,x2r-x3l-slant*(y2-y3));
 penpos1(alpha*(z_stem-tiny),0); penpos2(alpha*(z_stem-tiny),0);
 penpos3(alpha*(z_stem-tiny),0); penpos4(alpha*(z_stem-tiny),0);
 pair delta; delta=penoffset z3-z2 of currentpen;
 fill top lft z1l--z2l+delta---z3l+delta..lft z3l---lft z4l..bot z4l
  ---bot rt z4r--z3r-delta---z2r-delta..rt z2r---rt z1r..top z1r
  ---cycle;  % diagonal
 pickup crisp.nib; pos5(arm_thickness1,90); pos6(hair,180);
 top y5r=h; x5=x1; lft x6r=hround .75u; y6=good.y(y5l-beak/1.4)-eps;
 arm(5,6,a,beak_darkness,-.4beak_jut);  % upper arm and beak
 pos7(arm_thickness2,-90); pos8(hair,0);
 bot y7r=0; x7=x4; rt x8r=hround(w-.75u); y8=good.y(y7l+beak/1.2)+eps;
 arm(7,8,b,beak_darkness,.6beak_jut);  % lower arm and beak
 penlabels(1,2,3,4,5,6,7,8);
 hpos_corr=-.5u; put_accent;
repeat_once;
endchar;

ecchar "The z with hachek"; % from cshachel.mf (csfonts)
beginchar(oct"272",8u#,lc_hachek_height#,0);
italcorr x_height#*slant-.5serif_fit#-.3u#;
adjust_fit(0,.5serif_fit#);
numeric arm_thickness[],z_stem;
if hefty: arm_thickness1=Vround(vair-vair_corr); arm_thickness2=vair;
 z_stem=.6[vair,stem];
else: arm_thickness1=vair; arm_thickness2=vair'; z_stem=.9[vair,stem]; fi
pickup tiny.nib; rt x1r=rt x2r=hround(w-.8u); lft x3l=lft x4l=hround .5u;
top y1=x_height; y2=min(y1,x_height-2/3arm_thickness1);
bot y4=0; y3=max(y4,2/3arm_thickness2);
numeric alpha; alpha=diag_ratio(1,z_stem-tiny,y2-y3,x2r-x3l-slant*(y2-y3));
penpos1(alpha*(z_stem-tiny),0); penpos2(alpha*(z_stem-tiny),0);
penpos3(alpha*(z_stem-tiny),0); penpos4(alpha*(z_stem-tiny),0);
pair delta; delta=penoffset z3-z2 of currentpen;
fill top lft z1l--z2l+delta---z3l+delta..lft z3l---lft z4l..bot z4l
 ---bot rt z4r--z3r-delta---z2r-delta..rt z2r---rt z1r..top z1r
 ---cycle;  % diagonal
pickup crisp.nib; pos5(arm_thickness1,90); pos6(hair,180);
top y5r=x_height; x5=x1; lft x6r=hround .75u; y6=good.y(y5l-beak/1.4)-eps;
arm(5,6,a,beak_darkness,-.4beak_jut);  % upper arm and beak
pos7(arm_thickness2,-90); pos8(hair,0);
bot y7r=0; x7=x4; rt x8r=hround(w-.75u); y8=good.y(y7l+beak/1.2)+eps;
arm(7,8,b,beak_darkness,.6beak_jut);  % lower arm and beak
penlabels(1,2,3,4,5,6,7,8);
lc_hachek_accent(1/2[x6r,x1r]);
endchar;

ecchar "The letter z with dot above"; % from pl_ml (plfonts)
beginchar_twice(oct"273",8u#,x_height#,0);
 italcorr x_height#*slant-.5serif_fit#-.3u#;
 adjust_fit(0,.5serif_fit#);
 numeric arm_thickness[\\],z_stem;
 if hefty: arm_thickness1=Vround(vair-vair_corr); arm_thickness2=vair;
  z_stem=.6[vair,stem];
 else: arm_thickness1=vair; arm_thickness2=vair'; z_stem=.9[vair,stem]; fi
 pickup tiny.nib; rt x1r=rt x2r=hround(w-.8u); lft x3l=lft x4l=hround .5u;
 top y1=h; y2=min(y1,h-2/3arm_thickness1);
 bot y4=0; y3=max(y4,2/3arm_thickness2);
 numeric alpha;
 alpha=diag_ratio(1,z_stem-tiny,y2-y3,x2r-x3l-slant*(y2-y3));
 penpos1(alpha*(z_stem-tiny),0); penpos2(alpha*(z_stem-tiny),0);
 penpos3(alpha*(z_stem-tiny),0); penpos4(alpha*(z_stem-tiny),0);
 pair delta; delta=penoffset z3-z2 of currentpen;
 fill top lft z1l--z2l+delta---z3l+delta..lft z3l---lft z4l..bot z4l
  ---bot rt z4r--z3r-delta---z2r-delta..rt z2r---rt z1r..top z1r
  ---cycle;  % diagonal
 pickup crisp.nib; pos5(arm_thickness1,90); pos6(hair,180);
 top y5r=h; x5=x1; lft x6r=hround .75u; y6=good.y(y5l-beak/1.4)-eps;
 arm(5,6,a,beak_darkness,-.4beak_jut);  % upper arm and beak
 pos7(arm_thickness2,-90); pos8(hair,0);
 bot y7r=0; x7=x4; rt x8r=hround(w-.75u); y8=good.y(y7l+beak/1.2)+eps;
 arm(7,8,b,beak_darkness,.6beak_jut);  % lower arm and beak
 penlabels(1,2,3,4,5,6,7,8);
 put_dot(tiny,max(dot_size#,cap_curve#));
repeat_once;
endchar;

ecchar "The letter ij";
beginchar(oct"274",10.5u#,min(asc_height#,10/7x_height#+.5idot_diam#),desc_depth#);
italcorr h#*slant-serif_fit# + .5idot_diam# -2u#;
% adjust_fit(serif_fit#+stem_shift# if monospace:+.25u#fi,serif_fit#-stem_shift#);
pickup tiny.nib; pos1(stem',0); pos2(stem',0);
if odd(w-stem'): change_width; fi
lft x1l=hround(.25w-.5stem'); x1=x2;
top y1=x_height if serifs: +min(oo,serif_drop) fi; bot y2=0;
filldraw stroke z1e--z2e;  % stem
pos3(idot_diam,0); pos4(idot_diam,90);
if serifs: x3r=max(x1r,x1+.5(idot_diam-tiny)-.2jut) else: x3=x1-.5 fi;
top y4r=h+1;
if bot y4l-top y1<slab: y4l:=min(y4r-eps,y1+tiny+slab); fi
x3=x4; y3=.5[y4l,y4r]; dot(3,4);  % dot
if serifs: sloped_serif.l(1,2,a,1/3,.95jut,serif_drop);  % upper serif
 dish_serif(2,1,b,1/3,jut,c,1/3,.9jut); fi  % lower serif
%
adjust_fit(serif_fit#+2stem_shift# if monospace:+\\.5u# fi,
 serif_fit#-2stem_shift# if monospace:-.5u# fi);
pickup tiny.nib; pos11(stem',0); pos12(stem',0);
rt x11r=hround(.75w+.25u+.5stem'); x11=x12;
top y11=x_height if serifs: +min(oo,serif_drop) fi; bot y12=-1/3d;
filldraw stroke z11e--z12e;  % stem
pos13(idot_diam,0); pos14(idot_diam,90);
x13r=x11r; top y14r=h+1;
if bot y14l-top y11<slab: y14l:=min(y14r-eps,y11+tiny+slab); fi
x13=x14; y13=.5[y14l,y14r]; dot(13,14);  % dot
if serifs: sloped_serif.l(11,12,aa,1/3,1.1jut,serif_drop);  % upper serif
 pickup tiny.nib; pos15(vair,-90); pos16(hair,-180); pos17(flare,-180);
 x15=.5[x12,x16r]; bot y15r=-d-oo; y16-.5flare=-.88d;
 if monospace: lft x16r=.5w else: z16r=z17r; rt x17l=.5w+floor .75u fi;
 (x,y15r)=whatever[z15l,z12l]; x15r:=max(x,.5[x16r,x15]);
 filldraw stroke z12e{down}...z15e{left};  bulb(15,16,17); % arc and bulb
else: pickup fine.nib; pos12'(stem',0); z12'=z12;
 pos16(.2[vair,stem'],-90); pos17(vair,-90);
 lft x17r=hround .5w-.75u; bot y17r=vround 5/6(-d-oo);
 (x,y17l)=whatever[z17r,z13]; x17l:=x;
 z15r=z12'r; (x12'l,y15l)=whatever[z17l,z15r]; x15l=x12'l; y15=y15r;
 x16r=.5[x17r,x15r]; x16l:=.5[x17l,x15l]; bot y16r=-d-oo;
 filldraw stroke z12'e..{down}z15e & super_arc.e(15,16)
  & z16e{left}..z17e; fi  % arc and terminal
penlabels(1,2,3,4,5,6,11,12,13,14,15,16,17); endchar;


endinput;
